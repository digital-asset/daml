-- Copyright (c) 2019 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE AllowAmbiguousTypes #-}
{-# LANGUAGE NoImplicitPrelude #-}

daml 1.2
-- | MOVE Prelude DAML-LF primitives, just templates/contracts
module DA.Internal.Template(
    Template(ensure, signatory, observer, agreement),
    Choice(consuming, choiceController, choice), preconsuming, nonconsuming, postconsuming, NoEvent(..),
    stakeholder,
    create, exercise, fetch,
    archive, Archive(..),
    lookupByKey, fetchByKey, exerciseByKey,
    exerciseExplicit,
    IsParties(toParties),
    TemplateKey(key, maintainer)
    ) where

import GHC.Types (magic)
import DA.Internal.LF
import DA.Internal.Prelude

-- NOTE: use internalCreate vs create so that people implementing Template can't override create/fetch/exercise

-- | Create a contract based on a template.
create : Template c => c -> Update (ContractId c)
create = internalCreate

-- | HIDE
exerciseExplicit : Choice c e r => [Party] -> ContractId c -> e -> Update r
exerciseExplicit = internalExercise

-- | Exercise a contract choice.
exercise : forall c e r . Choice c e r => ContractId c -> e -> Update r
exercise c e = do
    cc <- fetch c
    exerciseExplicit (choiceController cc e) c e

-- | Fetch the contract data associated with the given contract id.
-- This fails and aborts the entire transaction if the `ContractId c`
-- supplied is not the contract ID of an active contract.
fetch : Template c => ContractId c -> Update c
fetch = internalFetch

-- | Archive the contract.
archive : Template c => ContractId c -> Update ()
archive c = exercise c Archive

-- | The stakeholders of a contract, i.e. its signatories and observers.
stakeholder : Template c => c -> [Party]
stakeholder c = signatory c ++ observer c

-- | Look up the contract id associated with a given contract key.
--
-- The `c` needs to be passed using an explicit type application. For
-- instance, if you want to look up a contract of template `Account` by its
-- key `k`, you must call `lookupByKey @Account k`.
lookupByKey : forall c k. TemplateKey c k => k -> Update (Optional (ContractId c))
lookupByKey = internalLookupByKey

-- | Fetch the contract id and contract data associated with a given
-- contract key.
--
-- The `c` needs to be passed using an explicit type application. For
-- instance, if you want to fetch a contract of template `Account` by its
-- key `k`, you must call `fetchByKey @Account k`.
fetchByKey : forall c k. TemplateKey c k => k -> Update (ContractId c, c)
-- NOTE(FM): the motivation for this function to return both the
-- contract id and the contract instance is that `fetchByKey` results in
-- a fetch node in the transaction structure, and the fetch node
-- contains the contract data, so we might as well include it here.
--
-- The reason why turning it into a fetch node is necessary is that:
-- 1. We want to have it a more relaxed authorization rule than
--    `lookupByKey`, which gets turned into a LookupByKey node;
-- 2. We want it to have the same authorization rules of a normal
--    fetch, and to implement _that_, we need to know what the
--    stakeholders of the fetched contract are, which requires
--    getting the contract instance.
fetchByKey k = fmap unpackPair (internalFetchByKey k)

-- | Exercise a choice on a contract given by its key.
--
-- The `c` needs to be passed using an explicit type application. For
-- instance, if you want to exercise a choice `Withdraw` on a contract of
-- template `Account` given by its key `k`, you must call
-- `exerciseByKey @Account k Withdraw`.
exerciseByKey : forall c k e r. (TemplateKey c k, Choice c e r) => k -> e -> Update r
exerciseByKey k e = do
    (c, cc) <- fetchByKey @c k
    exerciseExplicit (choiceController cc e) c e

class Template c where

    -- | Predicate that must hold for the succesful creation of the contract.
    ensure : c -> Bool
    ensure _ = True

    -- | The signatories of a contract.
    signatory : c -> [Party]

    -- | The observers of a contract.
    observer : c -> [Party]
    observer _ = []

    -- | The agreement text of a contract.
    agreement : c -> Text
    agreement _ = ""

    -- | HIDE
    internalCreate : c -> Update (ContractId c)
    internalCreate = magic @"create"

    -- | HIDE
    internalFetch : ContractId c -> Update c
    internalFetch = magic @"fetch"

    -- | HIDE
    internalArchive : [Party] -> ContractId c -> Update ()
    internalArchive = magic @"archive"


-- Deliberately not exported.
data Consuming = PreConsuming  -- Archive before executing exercise body.
               | PostConsuming -- Execute exercise body then archive.
               | NonConsuming  -- Don't archive.

-- | HIDE
nonconsuming : NoEvent c e -> Consuming
nonconsuming _ = NonConsuming

-- | HIDE
preconsuming : NoEvent c e -> Consuming
preconsuming _ = PreConsuming

-- | HIDE
postconsuming : NoEvent c e -> Consuming
postconsuming _ = PostConsuming

class Template c => Choice c e r | c e -> r where
    -- | HIDE
    consuming : NoEvent c e -> Consuming
    consuming = preconsuming

    choiceController : c -> e -> [Party]
    choice : c -> ContractId c -> e -> Update r

    -- | HIDE
    internalExercise : [Party] -> ContractId c -> e -> Update r
    internalExercise = magic @"exercise"

data Archive = Archive

instance Template c => Choice c Archive () where
    choiceController c _ = signatory c
    choice _ _ _ = return ()

    internalExercise p c _ = internalArchive p c

-- | HIDE
data NoEvent c e = NoEvent

-- | Accepted ways to specify a list of parties: either a single party, or a list of parties.
class IsParties a where
  -- | Convert to list of parties.
  toParties : a -> [Party]

instance IsParties Party where
  toParties p = [p]

instance IsParties [Party] where
  toParties ps = ps

class Template c => TemplateKey c k | c -> k where
    -- | The key of a contract.
    key : c -> k

    -- | The maintainers of the contract key.
    maintainer : c -> [Party]

    -- | HIDE
    internalFetchByKey : k -> Update (Pair "contractId" "contract" (ContractId c) c)
    internalFetchByKey = magic @"fetchByKey"

    -- | HIDE
    internalLookupByKey : k -> Update (Optional (ContractId c))
    internalLookupByKey = magic @"lookupByKey"
