# Copyright (c) 2019 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:haskell.bzl",
    "da_haskell_binary",
    "da_haskell_library",
    "da_haskell_test",
)
load(
    "//daml-foundations/daml-ghc/package-database:util.bzl",
    "daml_package_db",
    "daml_package_rule",
)

DAML_LF_VERSIONS = [
    "1.1",
    "1.2",
    "1.3",
    "1.dev",
]

daml_package_db(
    name = "package_db_for_daml-prim",
    daml_lf_versions = DAML_LF_VERSIONS,
    pkgs = [],
    visibility = ["//visibility:public"],
)

[
    daml_package_rule(
        name = "daml-prim-{}".format(ver),
        srcs = "//daml-foundations/daml-ghc/daml-prim-src",
        daml_lf_version = ver,
        package_db = ":package_db_for_daml-prim",
        pkg_name = "daml-prim",
        pkg_root = "daml-foundations/daml-ghc/daml-prim-src",
        visibility = ["//visibility:public"],
    )
    for ver in DAML_LF_VERSIONS
]

[
    daml_package_rule(
        name = "daml-stdlib-{}".format(ver),
        srcs = "//daml-foundations/daml-ghc/daml-stdlib-src",
        daml_lf_version = ver,
        dependencies = [":daml-prim-{}".format(ver)],
        package_db = ":package_db_for_daml-stdlib",
        pkg_name = "daml-stdlib",
        pkg_root = "daml-foundations/daml-ghc/daml-stdlib-src",
        visibility = ["//visibility:public"],
    )
    for ver in DAML_LF_VERSIONS
]

daml_package_db(
    name = "package_db_for_daml-stdlib",
    daml_lf_versions = DAML_LF_VERSIONS,
    pkgs = [":daml-prim-{}".format(ver) for ver in DAML_LF_VERSIONS],
    visibility = ["//visibility:public"],
)

daml_package_db(
    name = "package-db",
    daml_lf_versions = DAML_LF_VERSIONS,
    pkgs =
        [":daml-prim-{}".format(ver) for ver in DAML_LF_VERSIONS] +
        [":daml-stdlib-{}".format(ver) for ver in DAML_LF_VERSIONS],
    visibility = ["//visibility:public"],
)

# TODO Make sure that tools no longer rely on this.
# The daml-prim .dar archive. It is shipped as DAML-LF 1.0 such that we can link it from DAML-LF 1.0
# as well as DAML-LF 1.1 dalfs.
genrule(
    name = "gen-daml-prim.dar",
    srcs = ["//daml-foundations/daml-ghc/daml-prim-src"],
    outs = ["daml-prim.dar"],
    cmd = """
    $(location //daml-foundations/daml-tools/da-hs-damlc-app) \
      package daml-foundations/daml-ghc/daml-prim-src/LibraryModules.daml daml-prim --target 1.1 -o $(location daml-prim.dar)
  """,
    tools = ["//daml-foundations/daml-tools/da-hs-damlc-app"],
    visibility = ["//visibility:public"],
)
