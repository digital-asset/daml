# Copyright (c) 2019 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)

TEST_FILES = \
    [
        "BasicTests",
        "AuthorizedDivulgence",
        "DontDiscloseNonConsumingChoicesToObservers",
        "LargeTransaction",
        "ConjunctionChoices",
    ]

[
    daml_compile(
        name = name,
        main_src = "%s.daml" % name,
        target = "1.3",
        visibility = ["//daml-lf:__subpackages__"],
    )
    for name in TEST_FILES
]

daml_compile(
    name = "Optional",
    main_src = "Optional.daml",
    target = "1.3",
    visibility = ["//daml-lf:__subpackages__"],
)

[
    sh_test(
        name = name + "-test",
        size = "small",
        srcs = ["daml-lf-test.sh"],
        args = [
            "$(location //daml-lf/repl:repl)",
            "$(location //daml-foundations/daml-tools/da-hs-damlc-app)",
            "$(location :%s.dar)" % name,
        ],
        data = [
            "//daml-foundations/daml-tools/da-hs-damlc-app",
            "//daml-lf/repl",
            ":%s.dar" % name,
        ],
        deps = [
            "@bazel_tools//tools/bash/runfiles",
        ],
    )
    for name in TEST_FILES
]

[
    sh_test(
        name = "test-scenario-1-3-" + file.split("/")[2],
        size = "medium",
        srcs = ["scenario/test.sh"],
        args = [
            "$(location //daml-lf/repl:repl)",
            "$(location //daml-foundations/daml-tools/da-hs-damlc-app:da-hs-damlc-app)",
            "$(location :%s)" % file,
        ],
        data = [
            "//daml-foundations/daml-tools/da-hs-damlc-app",
            "//daml-lf/repl",
            file,
            "%s/EXPECTED.ledger" % "/".join(file.split("/")[0:3]),
        ],
        deps = [
            "@bazel_tools//tools/bash/runfiles",
        ],
    )
    for file in glob(["scenario/daml-1.3/*/Test.daml"])
]
