-- Copyright (c) 2019 The DAML Authors. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

daml 1.2

-- | `Validation` type and associated functions.
module DA.Validation where

import DA.NonEmpty as NE

-- | A `Validation` represents eithor a non-empty list of errors, or a successful value.
-- This generalizes `Either` to allow more than one error to be collected.
data Validation err a
  = Errors (NonEmpty err)
  | Success a

-- see if we need this
-- type ValidationText a = Validation Text a

-- | Fail for the given reason.
invalid : err -> Validation err a
invalid err = Errors $ singleton err

-- | Succeed with the given value.
ok : a -> Validation err a
ok a = Success a

-- | Turn an `Either` into a `Validation`.
validate : Either err a -> Validation err a
validate = either invalid ok

-- | Convert a `Validation err a` value into an `Either`,
-- taking the non-empty list of errors as the left value.
run : Validation err a -> Either (NonEmpty err) a
run (Errors errs) = Left errs
run (Success a)   = Right a

-- | Convert a `Validation err a` value into an `Either`,
-- taking just the first error as the left value.
run1 : Validation err a -> Either err a
run1 (Errors errs) = Left errs.hd
run1 (Success a)   = Right a

-- | Run a `Validation err a` with a default value in case of errors.
runWithDefault : a -> Validation err a -> a
runWithDefault d (Errors _) = d
runWithDefault _ (Success a) = a

instance Functor (Validation err) where
    fmap _ (Errors e) = Errors e
    fmap f (Success a) = Success (f a)

instance Applicative (Validation err) where
    pure = Success

    Success f <*> Success x = Success (f x)
    Success _ <*> Errors e2 = Errors e2
    Errors e1 <*> Success _ = Errors e1
    Errors e1 <*> Errors e2 = Errors (e1 <> e2)

    liftA2 f (Success x) (Success y) = Success (f x y)
    liftA2 _ (Errors e1) (Errors e2) = Errors (e1 <> e2)
    liftA2 _ (Errors e1) _ = Errors e1
    liftA2 _ _ (Errors e2) = Errors e2

    Success _m1 *> m2 = m2
    Errors e1 *> Success _ = Errors e1
    Errors e1 *> Errors e2 = Errors (e1 <> e2)

instance Action (Validation err) where
    (Success x) >>= k = k x
    (Errors e) >>= k = Errors e

instance ActionFail (Validation Text) where
    fail = Errors . singleton

-- | Convert an `Optional t` into a `Validation Text t`, or
-- more generally into an `m t` for any `ActionFail` type `m`.
(<?>) : ActionFail m => Optional b -> Text -> m b
None <?> s = fail s
Some v <?> _ = pure v

