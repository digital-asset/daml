-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE NoImplicitPrelude #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE AllowAmbiguousTypes #-}
{-# LANGUAGE CPP #-}

-- | MOVE Prelude interface functionality
module DA.Internal.Interface (
  HasInterfaceTypeRep(..),
  HasToInterface(..),
  HasFromInterface(..),
  Implements,
  interfaceTypeRep,
  toInterface,
  toInterfaceContractId,
  fromInterfaceContractId,
  coerceInterfaceContractId,
  fetchFromInterface,
  _exerciseInterfaceGuard,
  HasInterfaceView(..),
  view
) where

import DA.Internal.Prelude
import DA.Internal.Template.Functions
import DA.Internal.LF
import DA.Internal.Any
import GHC.Tuple.Check()

-- | (Daml-LF >= 1.15) Exposes the `interfaceTypeRep` function. Available only for interfaces.
class HasInterfaceTypeRep i where
  -- | HIDE
  _interfaceTypeRep : i -> TypeRep

-- | (Daml-LF >= 1.15) Obtain the `TemplateTypeRep` for the template given in the interface value.
interfaceTypeRep : HasInterfaceTypeRep i => i -> TemplateTypeRep
interfaceTypeRep x = TemplateTypeRep (_interfaceTypeRep x)

-- | (Daml-LF >= 1.15) Exposes the `toInterface` and `toInterfaceContractId` functions.
class HasToInterface t i where
  -- | HIDE
  _toInterface : t -> i

-- Note that this seems identical to the method '_toInterface'. The only difference
-- is the order of the type arguments. This allows `toInterface` to be type-applied to
-- the interface type first, which is usually more convenient.
-- i.e., for a value `asset` of template Asset which implements an interface Token,
--
-- @
--   token = toInterface @Token asset
-- @
--
-- This way, it's clear to readers what interface is being used, without needing
-- to provide/skip the template type argument, cf.
--
-- @
--   token = _toInterface @Asset @Token asset
--   token = _toInterface @_ @Token asset
-- @
--

-- | (Daml-LF >= 1.15) Convert a template value into an interface value.
-- For example `toInterface @MyInterface value` converts a template
-- `value` into a `MyInterface` type.
toInterface : forall i t. HasToInterface t i => t -> i
toInterface = _toInterface

-- | (Daml-LF >= 1.15) Exposes `fromInterface` and `fromInterfaceContractId`
-- functions.
class HasFromInterface t i where
  -- | (Daml-LF >= 1.15) Attempt to convert an interface value back into a
  -- template value. A `None` indicates that the expected template
  -- type doesn't match the underyling template type for the
  -- interface value.
  --
  -- For example, `fromInterface @MyTemplate value` will try to convert
  -- the interface value `value` into the template type `MyTemplate`.
  fromInterface : i -> Optional t

  -- | HIDE
  -- Like 'fromInterface', but raises a fatal error (`WronglyTypedContract`) if the
  -- underlying template type of the interface value doesn't match the desired type.
  unsafeFromInterface : ContractId i -> i -> t

-- | (Daml-LF >= 1.15) Constraint that indicates that a template implements an interface.
type Implements t i =
  ( HasInterfaceTypeRep i
  , HasToInterface t i
  , HasFromInterface t i
  )

-- | (Daml-LF >= 1.15) Convert a template contract id into an interface
-- contract id. For example, `toInterfaceContractId @MyInterface cid`.
toInterfaceContractId : forall i t. HasToInterface t i => ContractId t -> ContractId i
toInterfaceContractId = coerceContractId

-- | (Daml-LF >= 1.15) Convert an interface contract id into a template
-- contract id. For example, `fromInterfaceContractId @MyTemplate cid`.
--
-- Can also be used to convert an interface contract id into a contract id of
-- one of its requiring interfaces.
--
-- This function does not verify that the in/FAILterface contract id
-- actually points to a template of the given type. This means
-- that a subsequent `fetch`, `exercise`, or `archive` may fail, if,
-- for example, the contract id points to a contract that implements
-- the interface but is of a different template type than expected.
--
-- Therefore, you should only use `fromInterfaceContractId` in situations
-- where you already know that the contract id points to a contract of the
-- right template type. You can also use it in situations where you will
-- fetch, exercise, or archive the contract right away, when a transaction
-- failure is the appropriate response to the contract having the wrong
-- template type.
--
-- In all other cases, consider using `fetchFromInterface` instead.
fromInterfaceContractId : forall t i. HasFromInterface t i => ContractId i -> ContractId t
fromInterfaceContractId = coerceContractId

-- | (Daml-LF >= 1.15) Convert an interface contract id into a contract id of a
-- different interface. For example, given two interfaces `Source` and `Target`,
-- and `cid : ContractId Source`,
-- `coerceInterfaceContractId @Target @Source cid : ContractId Target`.
--
-- This function does not verify that the contract id
-- actually points to a contract that implements either interface. This means
-- that a subsequent `fetch`, `exercise`, or `archive` may fail, if,
-- for example, the contract id points to a contract of template `A` but it was
-- coerced into a `ContractId B` where `B` is an interface and there's no
-- interface instance B for A.
--
-- Therefore, you should only use `coerceInterfaceContractId` in situations
-- where you already know that the contract id points to a contract of the right
-- type. You can also use it in situations where you will fetch, exercise, or
-- archive the contract right away, when a transaction failure is the
-- appropriate response to the contract having the wrong type.
coerceInterfaceContractId : forall j i. (HasInterfaceTypeRep i, HasInterfaceTypeRep j) => ContractId i -> ContractId j
coerceInterfaceContractId = coerceContractId

-- | (Daml-LF >= 1.15) Fetch an interface and convert it to a specific
-- template type. If conversion is succesful, this function returns
-- the converted contract and its converted contract id. Otherwise,
-- this function returns `None`.
--
-- Can also be used to fetch and convert an interface contract id into a
-- contract and contract id of one of its requiring interfaces.
--
-- Example:
-- ```
-- do
--   fetchResult <- fetchFromInterface @MyTemplate ifaceCid
--   case fetchResult of
--     None -> abort "Failed to convert interface to appropriate template type"
--     Some (tplCid, tpl) -> do
--        ... do something with tpl and tplCid ...
-- ```
fetchFromInterface : forall t i. (HasFromInterface t i, HasFetch i) => ContractId i -> Update (Optional (ContractId t, t))
fetchFromInterface cid = do
    iface <- fetch cid
    case fromInterface @t iface of
      None -> pure None
      Some tpl -> pure (Some (fromInterfaceContractId @t cid, tpl))

#ifdef DAML_INTERFACE_EXTENDED
-- | HIDE The guard used for UExerciseInterface, during interface
-- desugaring. This function converts a guard on type t into a
-- guard on the type i, raising a WronglyTypedContract error if
-- there is a type mismatch. It has to work uniformly in these
-- three cases:
--
--  1. when t is a template type and i is an interface it implements
--  2. when t is an interface type and i is an interface it requires
--  3. when t and i are the same interface type
--
-- It supports these three cases uniformly because `unsafeFromInterface`
-- handles each of them separately.
_exerciseInterfaceGuard : forall i t.
  (HasFromInterface t i, HasInterfaceTypeRep i, HasTemplateTypeRep t) =>
  ContractId t -> (t -> Bool) -> i -> Bool
_exerciseInterfaceGuard cid tpred ivalue =
  tpred (unsafeFromInterface (coerceContractId cid) ivalue)
#else
_exerciseInterfaceGuard : forall t i a b c. a -> b -> c -> Bool
_exerciseInterfaceGuard cid tpred ivalue =
  error "Interfaces not supported in this version of Daml LF"
#endif

-- Read: Interface `i` has a view of type `v`
class HasInterfaceView i v | i -> v where
  _view : i -> v

-- Note that 'view' and '_view' really are identical, unlike 'toInterface'
-- and '_toInterface'. Still, it's probably a good idea to keep them separate
-- so we can change them more easily in the future.

view : forall i v. HasInterfaceView i v => i -> v
view = _view

