-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE ImplicitParams #-}
module DA.Stack
  ( CallStack
  , HasCallStack
  , SrcLoc(..)
  , callStack
  , getCallStack
  , prettyCallStack
  ) where

import Prelude
import DA.Text
import GHC.Stack.Types hiding (SrcLoc(..))
import qualified DA.Internal.Record
import qualified GHC.Stack.Types as GHC
import GHC.Tuple.Check()

-- | Pretty-print a `CallStack`.
prettyCallStack : CallStack -> Text
prettyCallStack = intercalate "\n" . prettyCallStackLines

prettyCallStackLines : CallStack -> [Text]
prettyCallStackLines cs = case getCallStack cs of
  []  -> []
  stk -> "CallStack (from HasCallStack):"
       :: map (("  " <>) . prettyCallSite) stk
  where
    prettyCallSite (f, loc) = f <> ", called at " <> prettySrcLoc loc

prettySrcLoc : SrcLoc -> Text
prettySrcLoc SrcLoc {..}
  = implode
      [ srcLocFile, ":"
      , show srcLocStartLine, ":"
      , show srcLocStartCol, " in "
      , srcLocPackage, ":", srcLocModule
      ]

-- | Extract the list of call sites from the `CallStack`.
--
-- The most recent call comes first.
getCallStack : CallStack -> [(Text, SrcLoc)]
getCallStack stk = case stk of
  EmptyCallStack            -> []
  PushCallStack (fn, loc, stk') -> (fromString fn,convSrcLoc loc) :: getCallStack stk'
  FreezeCallStack stk'      -> getCallStack stk'

-- | Access to the current `CallStack`.
callStack : HasCallStack => CallStack
callStack =
  case ?callStack of
    EmptyCallStack -> EmptyCallStack
    _              -> popCallStack ?callStack

-- | Location in the source code.
--
-- Line and column are 0-based.
data SrcLoc = SrcLoc
-- User-facing type using Text instead of TextLit.
  { srcLocPackage   : Text
  , srcLocModule    : Text
  , srcLocFile      : Text
  , srcLocStartLine : Int
  , srcLocStartCol  : Int
  , srcLocEndLine   : Int
  , srcLocEndCol    : Int
  }

instance DA.Internal.Record.HasField "srcLocPackage"   SrcLoc Text where
  getField SrcLoc {srcLocPackage} = srcLocPackage
  setField srcLocPackage' SrcLoc {..} = SrcLoc {srcLocPackage = srcLocPackage', ..}

instance DA.Internal.Record.HasField "srcLocModule"    SrcLoc Text where
  getField SrcLoc {srcLocModule} = srcLocModule
  setField srcLocModule' SrcLoc {..} = SrcLoc {srcLocModule = srcLocModule', ..}

instance DA.Internal.Record.HasField "srcLocFile"      SrcLoc Text where
  getField SrcLoc {srcLocFile} = srcLocFile
  setField srcLocFile' SrcLoc {..} = SrcLoc {srcLocFile = srcLocFile', ..}

instance DA.Internal.Record.HasField "srcLocStartLine" SrcLoc Int where
  getField SrcLoc {srcLocStartLine} = srcLocStartLine
  setField srcLocStartLine' SrcLoc {..} = SrcLoc {srcLocStartLine = srcLocStartLine', ..}

instance DA.Internal.Record.HasField "srcLocStartCol"  SrcLoc Int where
  getField SrcLoc {srcLocStartCol} = srcLocStartCol
  setField srcLocStartCol' SrcLoc {..} = SrcLoc {srcLocStartCol = srcLocStartCol', ..}

instance DA.Internal.Record.HasField "srcLocEndLine"   SrcLoc Int where
  getField SrcLoc {srcLocEndLine} = srcLocEndLine
  setField srcLocEndLine' SrcLoc {..} = SrcLoc {srcLocEndLine = srcLocEndLine', ..}

instance DA.Internal.Record.HasField "srcLocEndCol"    SrcLoc Int where
  getField SrcLoc {srcLocEndCol} = srcLocEndCol
  setField srcLocEndCol' SrcLoc {..} = SrcLoc {srcLocEndCol = srcLocEndCol', ..}

convSrcLoc : GHC.SrcLoc -> SrcLoc
convSrcLoc GHC.SrcLoc{..} =
  SrcLoc
    { srcLocPackage = fromString srcLocPackage
    , srcLocModule = fromString srcLocModule
    , srcLocFile = fromString srcLocFile
    , srcLocStartLine = srcLocStartLine - 1
    , srcLocStartCol = srcLocStartCol - 1
    , srcLocEndLine = srcLocEndLine - 1
    , srcLocEndCol = srcLocEndCol - 1
    }
