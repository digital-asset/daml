# Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("//bazel_tools:haskell.bzl", "common_haskell_exts", "da_haskell_library", "da_haskell_test")

da_haskell_library(
    name = "daml-lf-ast",
    srcs = glob(["src/**/*.hs"]),
    hackage_deps = [
        "aeson",
        "base",
        "bytestring",
        "containers",
        "deepseq",
        "Decimal",
        "extra",
        "filepath",
        "ghc-lib-parser",
        "hashable",
        "lens",
        "mtl",
        "recursion-schemes",
        "safe",
        "scientific",
        "template-haskell",
        "text",
        "time",
        "unordered-containers",
    ],
    src_strip_prefix = "src",
    visibility = ["//visibility:public"],
    deps = [
        "//libs-haskell/da-hs-base",
    ],
)

da_haskell_test(
    name = "tests",
    srcs = glob(["test/**/*.hs"]),
    hackage_deps = [
        "base",
        "containers",
        "extra",
        "tasty",
        "tasty-hunit",
    ],
    main_function = "DA.Daml.LF.Ast.Tests.main",
    src_strip_prefix = "test",
    visibility = ["//visibility:public"],
    deps = [
        ":daml-lf-ast",
        "//compiler/daml-lf-tools",
        "//libs-haskell/da-hs-base",
        "//libs-haskell/test-utils",
    ],
)

genquery(
    name = "deps",
    expression = "deps(//compiler/daml-lf-ast, 1) except //compiler/daml-lf-ast",
    opts = [
        "--output",
        "label_kind",
    ],
    scope = [":daml-lf-ast"],
    deps = [],
)

genrule(
    name = "cabel",
    srcs = [":deps"],
    outs = ["daml-lf-ast.cabal"],
    cmd = """
         cat << EOF > $@
-- This file was autogenerated
cabal-version: 2.4
name: {name}
build-type: Simple
version: 0.1.15.0
synopsis: {name}
license: Apache-2.0
author: Digital Asset
maintainer: Digital Asset
copyright: Digital Asset 2023
homepage: https://github.com/digital-asset/daml#readme
bug-reports: https://github.com/digital-asset/daml/issues

source-repository head
    type: git
    location: https://github.com/digital-asset/daml.git

library
    default-language: Haskell2010
    hs-source-dirs: src
    build-depends:
EOF

    grep -v "\\-\\-" $(SRCS) | \
      sed -E 's#^haskell_toolchain_library rule (@stackage//:([a-zA-Z0-9\\-]+))$$#      \\2, -- \\1#g' | \
      sed -E 's#^haskell_cabal_library rule (@stackage//:([a-zA-Z0-9\\-]+))$$#      \\2, -- \\1#g' | \
      sed -E 's#^_haskell_library rule (//[A-Za-z0-9/_\\-]+:([A-Za-z0-9/_\\-]+))$$#      \\2, -- \\1#g'  | \
       grep "\\-\\-" | sort >> $@

    echo '    exposed-modules:' >> $@

    grep -v "\\-\\-" $(SRCS) | \
       sed -E 's#^source file //[A-Za-z0-9/_\\-]+:src/([A-Za-z0-9/_\\-]+)\\.hs$$#      \\1 --#g' |
       grep "\\-\\-" | sort | \
       sed 's#/#\\.#g' >> $@

    echo -n '    default-extensions:' >> $@

    echo "{extenstions}" >> $@


     """.format(
        name = "daml-lf-ast",
        extenstions = "\n      ".join(common_haskell_exts),
    ),
)
