# Copyright (c) 2019 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
)
load(
    "//ledger/ledger-api-test-tool:conformance.bzl",
    "conformance_test",
    "server_conformance_test",
)
load(
    "//bazel_tools/client_server:client_server_build.bzl",
    "client_server_build",
)
load(
    "//ledger/participant-state/kvutils/tools:integrity_test.bzl",
    "integrity_test",
)
load("@os_info//:os_info.bzl", "is_windows")

da_scala_binary(
    name = "reference-v2",
    srcs = glob(["src/main/scala/**/*.scala"]),
    main_class = "com.daml.ledger.api.server.damlonx.reference.v2.ReferenceServer",
    resources = glob(["src/main/resources/**/*"]),
    visibility = ["//visibility:public"],
    runtime_deps = [
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_h2database_h2",
    ],
    deps = [
        "//daml-lf/archive:daml_lf_archive_reader",
        "//daml-lf/archive:daml_lf_dev_archive_java_proto",
        "//daml-lf/data",
        "//daml-lf/engine",
        "//language-support/scala/bindings",
        "//ledger/ledger-api-auth",
        "//ledger/ledger-api-common",
        "//ledger/ledger-api-health",
        "//ledger/participant-state",
        "//ledger/participant-state/kvutils",
        "//ledger/sandbox",
        "@maven//:com_github_scopt_scopt_2_12",
        "@maven//:com_typesafe_akka_akka_actor_2_12",
        "@maven//:com_typesafe_akka_akka_stream_2_12",
        "@maven//:io_dropwizard_metrics_metrics_core",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

########################################
### Testing the index server
########################################

da_scala_binary(
    name = "ephemeral-postgres-reference-server",
    srcs = glob(["src/test/scala/**/*.scala"]),
    data = [
        "@postgresql_dev_env//:all",
    ],
    main_class = "com.daml.ledger.api.server.damlonx.reference.v2.EphemeralPostgresReferenceServerMain",
    resources = ["src/main/resources/logback.xml"],
    visibility = ["//visibility:public"],
    deps = [
        ":reference-v2",
        "//ledger/sandbox:sandbox-scala-tests-lib",
    ],
)

SERVERS = {
    "h2database": {
        "binary": ":reference-v2",
        "server_args": [
            "--port=6865",
            # "db_close_delay=-1" is needed so that the in-memory database is not closed
            # (and therefore lost) after the flyway migration
            "--jdbc-url=jdbc:h2:mem:daml_on_x;db_close_delay=-1;db_close_on_exit=false",
            # participant-id contains special colon (:) character allowed in LfStrings
            "--participant-id=participant:h2database",
        ],
    },
    "postgres": {
        "binary": ":ephemeral-postgres-reference-server",
        "server_args": [
            "--port=6865",
        ],
    },
}

# Builds a ledger dump from running the test tool against a kvutils-based
# in-memory ledger.
client_server_build(
    name = "reference-ledger-dump",
    testonly = True,  # only test targets can depend on this.
    client = "//ledger/ledger-api-test-tool",
    client_args = [
        "--include=SemanticTests,ContractKeysIT,TransactionServiceIT",
        "--timeout-scale-factor=20",
        "localhost:6865",
    ],
    data = [
        "//ledger/test-common:dar-files",
    ],
    output_env = "KVUTILS_LEDGER_DUMP",
    server = ":reference-v2",
    server_args = [
        "--port 6865",
        "--jdbc-url jdbc:h2:mem:daml_on_x;db_close_delay=-1;db_close_on_exit=false",
    ],
    server_files = [
        "//ledger/test-common:dar-files",
    ],
) if not is_windows else None

# Test for checking the integrity of the ledger dump produces above.
integrity_test(
    name = "reference-ledger-dump-integrity-test",
    dump = ":reference-ledger-dump",
) if not is_windows else None

server_conformance_test(
    name = "conformance-test",
    servers = SERVERS,
    test_tool_args = [
        "--all-tests",
        "--exclude=LotsOfPartiesIT",
        "--exclude=TimeIT",
        "--exclude=ConfigManagementServiceIT",
    ],
)

conformance_test(
    name = "conformance-test-h2database-multi-node",
    ports = [
        6865,
        6866,
    ],
    server = ":reference-v2",
    server_args = [
        "--port=6865",
        # "db_close_delay=-1" is needed so that the in-memory database is not closed
        # (and therefore lost) after the flyway migration
        "--jdbc-url=jdbc:h2:mem:daml_on_x;db_close_delay=-1;db_close_on_exit=false",
        "--extra-participant=second-participant,6866,jdbc:h2:mem:daml_on_x2;db_close_delay=-1;db_close_on_exit=false",
    ],
    test_tool_args = [
        "--include=SemanticTests,TransactionServiceIT",
    ],
)

# Run the configuration management test in isolation as it modifies global ledger settings
# and hence cannot be run in parallel with other tests.
conformance_test(
    name = "conformance-test-h2database-config-management",
    ports = [6865],
    server = ":reference-v2",
    server_args = [
        "--port=6865",
        # "db_close_delay=-1" is needed so that the in-memory database is not closed
        # (and therefore lost) after the flyway migration
        "--jdbc-url=jdbc:h2:mem:daml_on_x;db_close_delay=-1;db_close_on_exit=false",
    ],
    test_tool_args = [
        "--include=ConfigManagementServiceIT",
    ],
)
