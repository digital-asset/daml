# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("//ledger/ledger-api-test-tool:conformance.bzl", "conformance_test")
load("@os_info//:os_info.bzl", "is_windows")

java_binary(
    name = "canton",
    main_class = "com.digitalasset.canton.CantonCommunityApp",
    runtime_deps = ["@canton//:lib"],
)

# Disabled on Windows because `coreutils` and `grpcurl` aren't easily available.
genrule(
    name = "canton-test-runner-with-dependencies-script",
    srcs = [
        ":canton-test-runner.sh",
    ],
    outs = ["canton-test-runner-with-dependencies.sh"],
    cmd = """
cat > $@ <<EOF
#!/usr/bin/env bash

set -euo pipefail

f=bazel_tools/tools/bash/runfiles/runfiles.bash
source "\\$${RUNFILES_DIR:-/dev/null}/\\$$f" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$${RUNFILES_MANIFEST_FILE:-/dev/null}" | cut -f2- -d' ')" 2>/dev/null || \\
  source "\\$$0.runfiles/\\$$f" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$$0.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$$0.exe.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \\
  { echo>&2 "ERROR: cannot find \\$$f"; exit 1; }; f=; set -e

PATH="\\$$(rlocation coreutils_nix/bin):\\$$(rlocation curl_nix/bin):\\$$(rlocation grpcurl_nix/bin):\\$$(rlocation jq_dev_env/bin):\\$$PATH"
export PATH

EOF
cat $< >> $@
""",
) if not is_windows else None

# Required because running `canton-test-runner-with-dependencies-script` directly fails.
sh_binary(
    name = "canton-test-runner-with-dependencies",
    srcs = [":canton-test-runner-with-dependencies-script"],
    # Ideally these would be part of the script definition above, but that doesn't seem to work.
    deps = ["@bazel_tools//tools/bash/runfiles"],
) if not is_windows else None

conformance_test(
    name = "conformance-test",
    # Ideally these would be part of the script definition above, but that doesn't seem to work.
    extra_data = [
        ":bootstrap.canton",
        ":canton",
        ":canton.conf",
        ":logback-debug.xml",
        "@coreutils_nix//:bin/base64",
        "@curl_nix//:bin/curl",
        "@grpcurl_nix//:bin/grpcurl",
        "@jq_dev_env//:jq",
    ],
    # FIXME: drop the following line once canton-test-runner supports LF 1.12
    lf_versions = ["legacy"],
    # lf_versions = ["stable", "latest"],
    ports = [
        5011,
        5021,
        5031,
        5041,
    ],
    runner = "@//bazel_tools/client_server/runner:runner",
    server = ":canton-test-runner-with-dependencies",
    test_tool_args = [
        "--verbose",
        "--concurrent-test-runs=4",  # lowered from default #procs to reduce flakes - details in https://github.com/digital-asset/daml/issues/7316
        # The following three contract key tests require uniqueness and are tested in conformance-test-contract-keys below
        "--exclude=ContractKeysIT,ContractKeysIT:CKFetchOrLookup,ContractKeysIT:CKNoFetchUndisclosed,ContractKeysIT:CKMaintainerScoped" +
        ",ParticipantPruningIT" +  # see "conformance-test-participant-pruning" below
        ",ConfigManagementServiceIT,LedgerConfigurationServiceIT" +  # dynamic config management not supported by Canton
        ",ClosedWorldIT" +  # Canton currently fails this test with a different error (missing namespace in "unallocated" party id)
        ",RaceConditionIT",
    ],
) if not is_windows else None

conformance_test(
    name = "conformance-test-contract-keys",
    # Ideally these would be part of the script definition above, but that doesn't seem to work.
    extra_data = [
        ":bootstrap.canton",
        ":canton",
        ":canton.conf",
        ":unique-contract-keys.conf",  # needed to pass all contract keys conformance tests
        ":logback-debug.xml",
        "@coreutils_nix//:bin/base64",
        "@curl_nix//:bin/curl",
        "@grpcurl_nix//:bin/grpcurl",
        "@jq_dev_env//:jq",
    ],
    # FIXME: drop the following line once canton-test-runner supports LF 1.12
    lf_versions = ["legacy"],
    # lf_versions = ["stable", "latest"],
    ports = [
        5011,
        5021,
        5031,
        5041,
    ],
    runner = "@//bazel_tools/client_server/runner:runner",
    server = ":canton-test-runner-with-dependencies",
    test_tool_args = [
        "--verbose",
        "--concurrent-test-runs=4",  # lowered from default #procs to reduce flakes - details in https://github.com/digital-asset/daml/issues/7316
        "--include=ContractKeysIT" +
        ",RaceConditionIT",
    ],
) if not is_windows else None

conformance_test(
    name = "conformance-test-participant-pruning",
    # Ideally these would be part of the script definition above, but that doesn't seem to work.
    extra_data = [
        ":bootstrap.canton",
        ":canton",
        ":canton.conf",
        ":enable-faster-pruning.conf",  # needed to prevent timing out on prune requests
        ":logback-debug.xml",
        "@coreutils_nix//:bin/base64",
        "@curl_nix//:bin/curl",
        "@grpcurl_nix//:bin/grpcurl",
        "@jq_dev_env//:jq",
    ],
    ports = [
        5011,
        5021,
        5031,
        5041,
    ],
    runner = "@//bazel_tools/client_server/runner:runner",
    server = ":canton-test-runner-with-dependencies",
    tags = [
        "manual",  # pruning test is flaky on canton because of safe-pruning offset reconciliation checks
    ],
    test_tool_args = [
        "--verbose",
        "--include=ParticipantPruningIT",
    ],
) if not is_windows else None
