# Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("//ledger/ledger-api-tests:conformance.bzl", "conformance_test")
load("@os_info//:os_info.bzl", "is_windows")

java_binary(
    name = "canton",
    main_class = "com.digitalasset.canton.CantonCommunityApp",
    runtime_deps = ["@canton//:lib"],
)

# Disabled on Windows because `coreutils` and `grpcurl` aren't easily available.
genrule(
    name = "canton-test-runner-with-dependencies-script",
    srcs = [
        ":canton-test-runner.sh",
    ],
    outs = ["canton-test-runner-with-dependencies.sh"],
    cmd = """
cat > $@ <<EOF
#!/usr/bin/env bash

set -euo pipefail

f=bazel_tools/tools/bash/runfiles/runfiles.bash
source "\\$${RUNFILES_DIR:-/dev/null}/\\$$f" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$${RUNFILES_MANIFEST_FILE:-/dev/null}" | cut -f2- -d' ')" 2>/dev/null || \\
  source "\\$$0.runfiles/\\$$f" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$$0.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \\
  source "\\$$(grep -sm1 "^\\$$f " "\\$$0.exe.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \\
  { echo>&2 "ERROR: cannot find \\$$f"; exit 1; }; f=; set -e

PATH="\\$$(rlocation coreutils_nix/bin):\\$$(rlocation curl_nix/bin):\\$$(rlocation grpcurl_nix/bin):\\$$(rlocation jq_dev_env/bin):\\$$PATH"
export PATH

EOF
cat $< >> $@
""",
) if not is_windows else None

# Required because running `canton-test-runner-with-dependencies-script` directly fails.
sh_binary(
    name = "canton-test-runner-with-dependencies",
    srcs = [":canton-test-runner-with-dependencies-script"],
    # Ideally these would be part of the script definition above, but that doesn't seem to work.
    deps = ["@bazel_tools//tools/bash/runfiles"],
) if not is_windows else None

conformance_test_extra_data_base = [
    ":canton_deploy.jar",
    ":logback-debug.xml",
    "@coreutils_nix//:bin/base64",
    "@curl_nix//:bin/curl",
    "@grpcurl_nix//:bin/grpcurl",
    "@jq_dev_env//:jq",
    "@bazel_tools//tools/jdk",
]

conformance_test_ports = [
    5011,
    5021,
    5031,
    5041,
]

conformance_test_tool_args = [
    "--verbose",
    "--concurrent-test-runs=1",  # lowered from default #procs to reduce flakes - details in https://github.com/digital-asset/daml/issues/7316
    "--timeout-scale-factor=2",  # increased to reduce flakes particularly wrt timeouts in TransactionService*IT tests
    "--exclude=" + ",".join([
        # dynamic config management not supported by Canton
        "ConfigManagementServiceIT",
        "LedgerConfigurationServiceIT",
        "ClosedWorldIT",  # Canton currently fails this test with a different error (missing namespace in "unallocated" party id)
        "ParticipantPruningIT",  # pruning not supported in Canton Community
        # tests with special config run in canton enterprise-repo
        "TLSOnePointThreeIT",
        "TLSAtLeastOnePointTwoIT",
        "CommandDeduplicationPeriodValidationIT:OffsetPruned",  # requires pruning not available in canton community
    ]),
]

conformance_test(
    name = "conformance-test",
    extra_data =
        conformance_test_extra_data_base + [
            ":bootstrap.canton",
            ":canton.conf",
        ],
    extra_runner_args = ["7000"],
    lf_versions = [
        "default",
        # TODO https://github.com/digital-asset/daml/issues/12051
        #  uncomment as soon as canton has a uptodate engine
        #"latest",
    ],
    ports = conformance_test_ports,
    runner = "@//bazel_tools/client_server/runner_with_health_check",
    server = ":canton-test-runner-with-dependencies",
    server_args = [
        "--config=$$(canonicalize_rlocation $(rootpath :canton.conf))",
        "--bootstrap=$$(canonicalize_rlocation $(rootpath :bootstrap.canton))",
    ],
    test_tool_args = conformance_test_tool_args,
) if not is_windows else None

conformance_test(
    name = "conformance-test-dev-domain",
    dev_mod_flag = "",
    extra_data =
        conformance_test_extra_data_base + [
            ":bootstrap-dev.canton",
            ":canton-dev.conf",
        ],
    extra_runner_args = ["7000"],
    lf_versions = [
        "default",
        "preview",
        "dev",
    ],
    ports = conformance_test_ports,
    preview_mod_flag = "",
    runner = "@//bazel_tools/client_server/runner_with_health_check",
    server = ":canton-test-runner-with-dependencies",
    server_args = [
        "--config=$$(canonicalize_rlocation $(rootpath :canton-dev.conf))",
        "--bootstrap=$$(canonicalize_rlocation $(rootpath :bootstrap-dev.canton))",
    ],
    test_tool_args = conformance_test_tool_args,
) if not is_windows else None
