# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_preview_version",
    "lf_stable_version",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
    "da_scala_library_suite",
)
load(
    "//language-support/scala/codegen:codegen.bzl",
    "dar_to_scala",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
    "da_scala_test_suite",
)
load(
    ":conformance.bzl",
    "conformance_test",
)

daml_compile(
    name = "PackageManagementTest",
    srcs = ["src/main/daml/PackageManagementTest.daml"],
    target = lf_stable_version,
    visibility = [
        "//:__subpackages__",
    ],
)

dar_to_scala(
    name = "PackageManagementTest.scala-codegen",
    srcs = [
        ":PackageManagementTest.dar",
    ],
    package_prefix = "com.daml.ledger.packagemanagementtest",
    srcjar_out = "PackageManagementTest.scala.srcjar",
    verbosity = 2,
)

da_scala_library(
    name = "PackageManagementTest.scala",
    srcs = [":PackageManagementTest.scala-codegen"],
    deps = [
        "//language-support/scala/bindings",
    ],
)

# Targets defined in //ledger/test-common
daml_lf_targets = [
    "-latest",
    "-dev",
] + [
    "-preview"
    for _ in lf_preview_version
]

# The ledger API test tool has compile time dependencies on the compiled DAML code,
# through the Scala codegen. The compiled DAR files are also bundled with the test tool binary.
#
# In order to allow running the ledger API test tool with the same DAML code compiled with
# different compiler versions, the following block duplicates the entire build pipeline:
# (Daml source code) -> (DAR files + codegen source files) -> (test tool binary)
[
    [
        genrule(
            name = "ledger-api-test-tool-dar-file-names%s" % lf_version_suffix,
            outs = ["DarsResourceNames%s.scala" % lf_version_suffix],
            cmd = """
cat > $@ <<EOF
package com.daml.ledger.api.testtool.infrastructure

import scala.collection.immutable

object DarsResourceNames {
  private[this] val suffix: String = \"%s\"
  val resources: immutable.Seq[String] = immutable.Seq(
    s\"/ledger/test-common/model-tests\\$$suffix.dar\",
    s\"/ledger/test-common/performance-tests\\$$suffix.dar\",
    s\"/ledger/test-common/semantic-tests\\$$suffix.dar\",
  )
}
EOF
            """ % lf_version_suffix,
        ),
        da_scala_library(
            name = "ledger-api-test-tool-lib%s" % lf_version_suffix,
            srcs = glob([
                "src/main/scala/com/daml/ledger/api/testtool/infrastructure/**/*.scala",
            ]) + [
                ":DarsResourceNames%s.scala" % lf_version_suffix,
            ],
            # generated_srcs is required for scaladoc
            generated_srcs = [
                ":DarsResourceNames%s.scala" % lf_version_suffix,
            ],
            visibility = [
                "//:__subpackages__",
            ],
            deps = [
                "//daml-lf/data",
                "//language-support/scala/bindings",
                "//ledger/ledger-api-common",
                "//ledger/test-common",
                "//ledger/test-common:model-tests%s.scala" % lf_version_suffix,
                "//libs-scala/grpc-utils",
                "//libs-scala/resources",
                "//libs-scala/resources-akka",
                "//libs-scala/resources-grpc",
                "//libs-scala/timer-utils",
                "@maven//:ai_x_diff_2_12",
                "@maven//:com_typesafe_akka_akka_actor_2_12",
                "@maven//:com_typesafe_akka_akka_stream_2_12",
                "@maven//:com_typesafe_config",
                "@maven//:io_netty_netty_common",
                "@maven//:io_netty_netty_transport",
                "@maven//:org_scala_lang_modules_scala_collection_compat_2_12",
                "@maven//:org_scala_lang_modules_scala_java8_compat_2_12",
                "@maven//:org_slf4j_slf4j_api",
            ],
        ),
        da_scala_library_suite(
            name = "ledger-api-test-tool-test-suites%s" % lf_version_suffix,
            srcs = glob(["src/main/scala/com/daml/ledger/api/testtool/suites/**/*.scala"]),
            visibility = [
                "//:__subpackages__",
            ],
            deps = [
                ":PackageManagementTest.scala",
                ":ledger-api-test-tool-lib%s" % lf_version_suffix,
                "//daml-lf/data",
                "//language-support/scala/bindings",
                "//ledger/ledger-api-common",
                "//ledger/test-common",
                "//ledger/test-common:model-tests%s.scala" % lf_version_suffix,
                "//ledger/test-common:performance-tests%s.scala" % lf_version_suffix,
                "//ledger/test-common:semantic-tests%s.scala" % lf_version_suffix,
                "//libs-scala/grpc-utils",
                "//libs-scala/timer-utils",
                "@maven//:ai_x_diff_2_12",
                "@maven//:com_chuusai_shapeless_2_12",
                "@maven//:io_grpc_grpc_api",
                "@maven//:io_grpc_grpc_context",
                "@maven//:org_cvogt_scala_extensions_2_12",
                "@maven//:org_scala_lang_modules_scala_collection_compat_2_12",
                "@maven//:org_slf4j_slf4j_api",
            ],
        ),
        da_scala_library(
            name = "ledger-api-test-tool-tests%s" % lf_version_suffix,
            srcs = glob(["src/main/scala/com/daml/ledger/api/testtool/tests/**/*.scala"]),
            visibility = [
                "//:__subpackages__",
            ],
            deps = [
                ":ledger-api-test-tool-lib%s" % lf_version_suffix,
                ":ledger-api-test-tool-test-suites%s" % lf_version_suffix,
                "//ledger/ledger-api-common",
            ],
        ),
        da_scala_binary(
            name = "ledger-api-test-tool%s" % lf_version_suffix,
            srcs = glob([
                "src/main/scala/com/daml/ledger/api/testtool/*.scala",
            ]) + [
                ":DarsResourceNames%s.scala" % lf_version_suffix,
            ],
            # generated_srcs is required for scaladoc
            generated_srcs = [
                ":DarsResourceNames%s.scala" % lf_version_suffix,
            ],
            main_class = "com.daml.ledger.api.testtool.LedgerApiTestTool",
            resources = [
                "src/main/resources/logback.xml",
                ":PackageManagementTest.dar",
                "//ledger/test-common:dar-files%s" % lf_version_suffix,
            ],
            scala_deps = [
                "@maven//:org_scala_lang_modules_scala_collection_compat",
            ],
            tags = [
                "maven_coordinates=com.daml:ledger-api-test-tool%s:__VERSION__" % lf_version_suffix,
                # We release this as a fat jar so this tag ensures that the dependencies in the generated
                # POM file are set correctly.
                "fat_jar",
                "no_scala_version_suffix",
            ],
            visibility = ["//visibility:public"],
            runtime_deps = [
                "@maven//:ch_qos_logback_logback_classic",
            ],
            deps = [
                ":ledger-api-test-tool-lib%s" % lf_version_suffix,
                ":ledger-api-test-tool-tests%s" % lf_version_suffix,
                "//ledger/ledger-api-common",
                "//libs-scala/build-info",
                "//libs-scala/resources",
                "//libs-scala/resources-akka",
                "//libs-scala/resources-grpc",
                "@maven//:com_github_scopt_scopt_2_12",
                "@maven//:io_grpc_grpc_api",
                "@maven//:io_grpc_grpc_core",
                "@maven//:io_grpc_grpc_netty",
                "@maven//:io_netty_netty_handler",
                "@maven//:org_slf4j_slf4j_api",
            ],
        ),
    ]
    for lf_version_suffix in [""] + daml_lf_targets
]

conformance_test(
    name = "ssl-test",
    extra_data = [
        "//ledger/test-common/test-certificates:client.crt",
        "//ledger/test-common/test-certificates:client.pem",
        "//ledger/test-common/test-certificates:server.crt",
        "//ledger/test-common/test-certificates:server.pem",
        "//ledger/test-common/test-certificates:ca.crt",
    ],
    server = "//ledger/ledger-on-memory:app",
    server_args = [
        "--contract-id-seeding=testing-weak",
        "--participant participant-id=ssl-test,port=6865",
        "--crt $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:server.crt))",
        "--cacrt $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:ca.crt))",
        "--pem $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:server.pem))",
    ],
    test_tool_args = [
        "--verbose",
        "--include=SemanticTests",
        "--crt $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:client.crt))",
        "--cacrt $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:ca.crt))",
        "--pem $$(rlocation $$TEST_WORKSPACE/$(rootpath //ledger/test-common/test-certificates:client.pem))",
    ],
)

# Explicitly include retired tests here to make sure existing CI pipelines are not broken
# Retired tests will be eventually removed
conformance_test(
    name = "retired-tests",
    server = "//ledger/ledger-on-memory:app",
    server_args = [
        "--contract-id-seeding=testing-weak",
        "--participant participant-id=ssl-test,port=6865",
    ],
    test_tool_args = [
        "--include=LotsOfPartiesIT",
        "--include=TransactionScaleIT",
    ],
)

# Makes sure that deprecated CLI options can still be used to make sure existing CI pipelines are not broken
# This test should fail if any deprecated CLI option has any effect whatsoever -- they are preserved
# exclusively for backwards-compatibility
# Deprecated CLI options will be eventually removed
conformance_test(
    name = "deprecated-cli-options",
    server = "//ledger/ledger-on-memory:app",
    server_args = [
        "--contract-id-seeding=testing-weak",
        "--participant participant-id=ssl-test,port=6865",
    ],
    test_tool_args = [
        "--include=IdentityIT",
        "--all-tests",
        "--load-scale-factor=THIS_OPTION_IS_DEPRECATED_AND_HAS_NO_EFFECT",
        "--target-port=THIS_OPTION_IS_DEPRECATED_AND_HAS_NO_EFFECT",
    ],
)

# Test that both --include ContractKeysIT as well as --include ContractKeysIT:CKFetchOrLookup
# are supported.
conformance_test(
    name = "test-name-syntax",
    server = "//ledger/ledger-on-memory:app",
    server_args = [
        "--contract-id-seeding=testing-weak",
        "--participant participant-id=test-name,port=6865",
    ],
    test_tool_args = [
        "--include=IdentityIT",
        "--include=IdentityIT:IdNotEmpty",
    ],
)

# Test that runs the whole ledger API test suite once for each listed compiler version
[
    [
        conformance_test(
            name = "conformance-test-lf-version%s" % daml_lf_target,
            client = "//ledger/ledger-api-test-tool:ledger-api-test-tool%s" % daml_lf_target,
            server = "//ledger/ledger-on-memory:app",
            server_args = [
                "--daml-lf-dev-mode-unsafe",  # Enables the use of all known compiler versions
                "--contract-id-seeding=testing-weak",
                "--participant participant-id=conformance-test-dev-mode,port=6865",
            ],
            test_tool_args = [
                "--verbose",
            ],
        ),
    ]
    for daml_lf_target in daml_lf_targets
]
