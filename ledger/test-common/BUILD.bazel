# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
    "silencer_plugin",
)
load(
    "//language-support/scala/codegen:codegen.bzl",
    "dar_to_scala",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_dev_version",
    "lf_latest_version",
    "lf_preview_version",
    "lf_stable_version",
)
load("@scala_version//:index.bzl", "scala_major_version")

# Tuples of (lf_version, name_suffix)
# The DAML files are compiled once for each daml-lf version listed here.
test_common_configurations = [
    (lf_stable_version, ""),
    (lf_latest_version, "-latest"),
    (lf_dev_version, "-dev"),
] + [
    (v, "-preview")
    for v in lf_preview_version
]

da_scala_library(
    name = "test-common",
    srcs = glob(["src/main/scala/**/*.scala"]),
    visibility = [
        "//visibility:public",
    ],
    deps = [
        "//ledger-api/grpc-definitions:ledger_api_proto_scala",
        "//libs-scala/timer-utils",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:io_grpc_grpc_context",
    ],
)

[
    [
        daml_compile(
            name = "%s-tests%s" % (test_name, target_suffix),
            srcs = glob(["src/main/daml/%s/*.daml" % test_name]),
            target = target,
            visibility = ["//visibility:public"],
        ),
        dar_to_scala(
            name = "%s-tests%s.scala-codegen" % (test_name, target_suffix),
            srcs = [":%s-tests%s.dar" % (test_name, target_suffix)],
            package_prefix = "com.daml.ledger.test.%s" % test_name,
            srcjar_out = "%s%s.scala.srcjar" % (test_name, target_suffix),
            verbosity = 2,
            visibility = ["//visibility:public"],
        ),
        da_scala_library(
            name = "%s-tests%s.scala" % (test_name, target_suffix),
            srcs = [":%s-tests%s.scala-codegen" % (test_name, target_suffix)],
            plugins = [silencer_plugin],
            scala_deps = extra_deps,
            scalacopts = [
                "-P:silencer:lineContentFilters=import _root_.scala.language.higherKinds;",
            ],
            visibility = ["//visibility:public"],
            deps = [
                "//language-support/scala/bindings",
            ],
        ),
    ]
    for (test_name, extra_deps) in [
        (
            "model",
            ["@maven//:org_scala_lang_modules_scala_collection_compat"],
        ),
        (
            "semantic",
            ["@maven//:org_scala_lang_modules_scala_collection_compat"],
        ),
        ("performance", []),
    ]
    for (target, target_suffix) in test_common_configurations
]

[
    [
        filegroup(
            name = "dar-files%s" % target_suffix,
            srcs = [
                ":model-tests%s.dar" % target_suffix,
                ":performance-tests%s.dar" % target_suffix,
                ":semantic-tests%s.dar" % target_suffix,
            ],
            visibility = ["//visibility:public"],
        ),
    ]
    for (target, target_suffix) in test_common_configurations
]
