# Copyright (c) 2019 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
)
load(
    "//language-support/scala/codegen:codegen.bzl",
    "dar_to_scala",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_dev_version",
    "lf_latest_version",
    "lf_stable_version",
)

da_scala_library(
    name = "test-common",
    srcs = glob(["src/main/scala/**/*.scala"]),
    visibility = [
        "//visibility:public",
    ],
    deps = [
        "//ledger-api/grpc-definitions:ledger-api-scalapb",
        "//libs-scala/direct-execution-context",
        "//libs-scala/timer-utils",
        "@maven//:io_grpc_grpc_api",
        "@maven//:io_grpc_grpc_context",
        "@maven//:io_grpc_grpc_core",
    ],
)

daml_compile(
    name = "SemanticTests",
    main_src = "src/main/daml/SemanticTests.daml",
    target = lf_stable_version,
    visibility = ["//visibility:public"],
)

dar_to_scala(
    name = "SemanticTests.scala-codegen",
    srcs = [
        ":SemanticTests.dar",
    ],
    package_prefix = "com.digitalasset.ledger.test",
    srcjar_out = "SemanticTests.scala.srcjar",
    verbosity = 2,
    visibility = ["//visibility:public"],
)

da_scala_library(
    name = "SemanticTests.scala",
    srcs = [":SemanticTests.scala-codegen"],
    visibility = ["//visibility:public"],
    deps = [
        "//language-support/scala/bindings",
    ],
)

lf_test_versions = [
    (lf_stable_version, "stable"),
    (lf_latest_version, "latest"),
    (lf_dev_version, "dev"),
]

[
    [
        daml_compile(
            name = "Test-%s" % target_name,
            main_src = "src/main/daml/Test.daml",
            target = target,
            visibility = ["//visibility:public"],
        ),
        dar_to_scala(
            name = "Test-%s.scala-codegen" % target_name,
            srcs = [":Test-%s.dar" % target_name],
            package_prefix = "com.digitalasset.ledger.test_%s" % target_name,
            srcjar_out = "Test-%s.scala.srcjar" % target_name,
            verbosity = 2,
            visibility = ["//visibility:public"],
        ),
        da_scala_library(
            name = "Test-%s.scala" % target_name,
            srcs = [":Test-%s.scala-codegen" % target_name],
            visibility = ["//visibility:public"],
            deps = [
                "//language-support/scala/bindings",
            ],
        ),
    ]
    for (target, target_name) in lf_test_versions
]

filegroup(
    name = "dar-files",
    srcs = [
        ":SemanticTests.dar",
        ":Test-dev.dar",
        ":Test-stable.dar",
    ],
    visibility = ["//visibility:public"],
)
