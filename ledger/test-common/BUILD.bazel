# Copyright (c) 2020 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
)
load(
    "//language-support/scala/codegen:codegen.bzl",
    "dar_to_scala",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_stable_version",
)

da_scala_library(
    name = "test-common",
    srcs = glob(["src/main/scala/**/*.scala"]),
    visibility = [
        "//visibility:public",
    ],
    deps = [
        "//ledger-api/grpc-definitions:ledger_api_proto_scala",
        "//libs-scala/timer-utils",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:io_grpc_grpc_context",
    ],
)

[
    [
        daml_compile(
            name = "%s-tests" % test_name,
            srcs = glob(["src/main/daml/%s/*.daml" % test_name]),
            target = target,
            visibility = ["//visibility:public"],
        ),
        dar_to_scala(
            name = "%s-tests.scala-codegen" % test_name,
            srcs = [":%s-tests.dar" % test_name],
            package_prefix = "com.daml.ledger.test.%s" % test_name,
            srcjar_out = "%s.scala.srcjar" % test_name,
            verbosity = 2,
            visibility = ["//visibility:public"],
        ),
        da_scala_library(
            name = "%s-tests.scala" % test_name,
            srcs = [":%s-tests.scala-codegen" % test_name],
            visibility = ["//visibility:public"],
            deps = ["//language-support/scala/bindings"],
        ),
    ]
    for (test_name, target) in [
        ("model", lf_stable_version),
        ("semantic", lf_stable_version),
        ("performance", lf_stable_version),
    ]
]

filegroup(
    name = "dar-files",
    srcs = [
        ":model-tests.dar",
        ":performance-tests.dar",
        ":semantic-tests.dar",
    ],
    visibility = ["//visibility:public"],
)
