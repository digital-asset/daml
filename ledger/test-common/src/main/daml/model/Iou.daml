-- Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0


module Iou where

type IouCid = ContractId Iou

template Iou
  with
    issuer : Party
    owner : Party
    currency : Text
    amount : Decimal
    observers : [Party]
  where
    ensure amount > 0.0

    signatory issuer, owner

    observer observers

    controller owner can

      -- Split the IOU by dividing the amount.
      Iou_Split : (IouCid, IouCid)
         with
          splitAmount: Decimal
        do
          let restAmount = amount - splitAmount
          splitCid <- create this with amount = splitAmount
          restCid <- create this with amount = restAmount
          return (splitCid, restCid)

      -- Merge two IOUs by aggregating their amounts.
      Iou_Merge : IouCid
        with
          otherCid: IouCid
        do
          otherIou <- fetch otherCid
          -- Check the two IOU's are compatible
          assert (
            currency == otherIou.currency &&
            owner == otherIou.owner &&
            issuer == otherIou.issuer
            )
          -- Retire the old Iou
          archive otherCid
          -- Return the merged Iou
          create this with amount = amount + otherIou.amount

      Iou_Transfer : ContractId IouTransfer
        with
          newOwner : Party
        do create IouTransfer with iou = this; newOwner

      Iou_AddObserver : IouCid
        with
          newObserver : Party
        do create this with observers = newObserver :: observers

      Iou_RemoveObserver : IouCid
        with
          oldObserver : Party
        do create this with observers = filter (/= oldObserver) observers

template IouTransfer
  with
    iou : Iou
    newOwner : Party
  where
    signatory iou.issuer, iou.owner

    controller iou.owner can
      IouTransfer_Cancel : IouCid
        do create iou

    controller newOwner can
      IouTransfer_Reject : IouCid
        do create iou

      IouTransfer_Accept : IouCid
        do
          create iou with
            owner = newOwner
            observers = []
