# Copyright (c) 2019 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
    "da_scala_test_suite",
)

navigator_scalacopts = [
    "-Xelide-below",
    "CONFIG",
    "-Xsource:2.13",
]

common_jvm_flags = [
    "-Xmx2G",
]

# All frontend resource files.
# These come in a manually created JAR file, this rule is just wrapping it in
# a java_import, so that it is a valid target for the 'resources' property of
# scala_binary.
java_import(
    name = "frontend-resources",
    jars = [
        "//navigator/frontend:frontend.jar",
    ],
)

# Version file, as a top level resources.
java_library(
    name = "version-resource",
    resources = [
        "//:component-version",
    ],
)

# Revision file, as a top level resources.
java_library(
    name = "revision-resource",
    resources = [
        "//:git-revision",
    ],
)

# Fake git revision, using SHA-1 of the empty string
# Used in tests, to make sure we don't rerun tests on every commit.
genrule(
    name = "test-revision",
    srcs = [],
    outs = [".git-revision"],
    cmd = "echo \"da39a3ee5e6b4b0d3255bfef95601890afd80709\" > \"$@\"",
)

# Fake git revision file, as a top level resource.
# Used in tests, to make sure we don't rerun tests on every commit.
java_library(
    name = "test-revision-resource",
    resource_strip_prefix = "navigator/backend",
    resources = [
        ":test-revision",
    ],
)

# Static backend resources.
java_library(
    name = "backend-resources",
    resources = glob(["src/main/resources/**/*"]),
)

compileDependencies = [
    "//daml-lf/archive:daml_lf_archive_reader",
    "//daml-lf/archive:daml_lf_dev_archive_java_proto",
    "//daml-lf/data",
    "//daml-lf/transaction",
    "//daml-lf/interface",
    "//daml-lf/language",
    "//ledger-api/rs-grpc-bridge",
    "//ledger-api/rs-grpc-akka",
    "//language-support/scala/bindings",
    "//ledger/ledger-api-common",
    "//ledger/ledger-api-client",
    "//ledger/ledger-api-domain",
    "//ledger/ledger-api-grpc-utils",
    "//ledger-service/lf-value-json",
    "//daml-assistant/scala-daml-project-config",
    "@maven//:com_chuusai_shapeless_2_12",
    "@maven//:com_github_pureconfig_pureconfig_2_12",
    "@maven//:com_github_scopt_scopt_2_12",
    "@maven//:com_lihaoyi_sourcecode_2_12",
    "@maven//:com_typesafe_akka_akka_actor_2_12",
    "@maven//:com_typesafe_akka_akka_http_2_12",
    "@maven//:com_typesafe_akka_akka_http_core_2_12",
    "@maven//:com_typesafe_akka_akka_http_spray_json_2_12",
    "@maven//:com_typesafe_akka_akka_slf4j_2_12",
    "@maven//:com_typesafe_akka_akka_stream_2_12",
    "@maven//:com_typesafe_akka_akka_stream_testkit_2_12",
    "@maven//:com_typesafe_config",
    "@maven//:ch_qos_logback_logback_classic",
    "@maven//:com_typesafe_scala_logging_scala_logging_2_12",
    "@maven//:io_netty_netty_tcnative_boringssl_static",
    "@maven//:io_netty_netty_handler",
    "@maven//:io_grpc_grpc_netty",
    "@maven//:io_spray_spray_json_2_12",
    "@maven//:org_sangria_graphql_sangria_2_12",
    "@maven//:org_sangria_graphql_sangria_marshalling_api_2_12",
    "@maven//:org_sangria_graphql_sangria_spray_json_2_12",
    "@maven//:org_scalaz_scalaz_core_2_12",
    "@maven//:org_slf4j_slf4j_api",
    "@maven//:org_jline_jline",
    "@maven//:org_jline_jline_reader",
    "@maven//:org_scala_lang_modules_scala_parser_combinators_2_12",
    "@maven//:org_gnieh_diffson_core_2_12",
    "@maven//:org_gnieh_diffson_spray_json_2_12",
    "@maven//:org_tpolecat_doobie_core_2_12",
    "@maven//:org_tpolecat_doobie_free_2_12",
    "@maven//:org_typelevel_cats_core_2_12",
    "@maven//:org_typelevel_cats_effect_2_12",
    "@maven//:org_typelevel_cats_free_2_12",
    "@maven//:org_typelevel_cats_kernel_2_12",
    "@maven//:org_xerial_sqlite_jdbc",
]

testDependencies = [
    ":navigator-library",
    "@maven//:com_typesafe_akka_akka_http_testkit_2_12",
    "@maven//:org_scalatest_scalatest_2_12",
    "@maven//:org_scalacheck_scalacheck_2_12",
    "@maven//:org_slf4j_slf4j_simple",
] + compileDependencies

da_scala_library(
    name = "navigator-library",
    srcs = glob([
        "src/main/scala/com/digitalasset/navigator/**/*.scala",
    ]),
    scalacopts = navigator_scalacopts,
    deps = compileDependencies,
)

da_scala_library(
    name = "navigator-tests-library",
    srcs = glob(
        ["src/test/**/*.scala"],
        exclude = [
            "src/test/**/*Spec.scala",
            "src/test/**/*Test.scala",
        ],
    ),
    scalacopts = navigator_scalacopts,
    deps = testDependencies,
)

da_scala_binary(
    name = "navigator-binary",
    main_class = "com.digitalasset.navigator.NavigatorBackend",
    tags = [
        "maven_coordinates=com.digitalasset:navigator:__VERSION__",
        "no_scala_version_suffix",
    ],
    visibility = [
        "//visibility:public",
    ],
    deps = [
        ":backend-resources",
        ":frontend-resources",
        ":navigator-library",
        ":revision-resource",
        ":version-resource",
    ],
)

# Static test resources.
java_library(
    name = "test-resources",
    resources = glob(["src/test/resources/**/*"]),
)

da_scala_test_suite(
    name = "navigator-scala-tests",
    size = "small",
    srcs = glob([
        "src/test/**/*Spec.scala",
        "src/test/**/*Test.scala",
    ]),
    scalacopts = navigator_scalacopts,
    deps = [
        ":navigator-tests-library",
        ":test-resources",
        ":version-resource",
        ":test-revision-resource",
    ] + testDependencies,
)
