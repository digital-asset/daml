# Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
    "lf_scalacopts_stricter",
)
load("//:canton_dep.bzl", "canton")
load("@build_environment//:configuration.bzl", "artif_auth")

java_binary(
    name = "canton",
    main_class = "com.digitalasset.canton.CantonCommunityApp",
    visibility = ["//visibility:public"],
    runtime_deps = [
        # Do not sort
        "//canton:lib",
    ],
)

# We replace engine and archive classes in canton
# This should be used for testing only
java_binary(
    name = "canton-patched",
    main_class = "com.digitalasset.canton.CantonCommunityApp",
    visibility = ["//visibility:public"],
    runtime_deps = [
        # The following prevents buildifier from sorting the deps
        # Do not sort
        "//daml-lf/engine",
        "//daml-lf/archive:daml_lf_archive_reader",
        "//canton:lib",
    ],
)

genrule(
    name = "canton-lib",
    srcs = [":lib/canton.jar"] if canton["local"] else ["@canton//:jar"],
    outs = ["canton-lib.jar"],
    cmd = """
    set -euo pipefail

    cp $(location {src}) $@
    """.format(src = ":lib/canton.jar" if canton["local"] else "@canton//:jar"),
    visibility = ["//visibility:public"],
)

java_import(
    name = "lib",
    jars = [":canton-lib.jar"],
    visibility = ["//visibility:public"],
)

java_import(
    name = "lib-ee",
    jars = [":canton-lib-ee.jar"],
    tags = ["canton-ee"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "canton-lib-ee",
    outs = ["canton-lib-ee.jar"],
    cmd = """
set -euo pipefail


CANTON_ENTERPRISE_VERSION=2.7.0-snapshot.20230620.10605.0.vb9879533
CANTON_ENTERPRISE_SHA=6a62452cb78b37a4b7c37efe22839ab8b19963e90ad71be6a7a2cfdbe0ca3833

url=https://digitalasset.jfrog.io/artifactory/canton-enterprise/canton-enterprise-$$CANTON_ENTERPRISE_VERSION.tar.gz

curl=$(location @curl_dev_env//:bin/curl)

tmp=$$(mktemp)
$$curl --location \
     --fail \
     --insecure \
     --silent \
     -H "Authorization: Basic {auth}" \
     $$url \
 > $$tmp

computed_sha=$$(sha256sum $$tmp | awk '{{print $$1}}')
if [ "$$CANTON_ENTERPRISE_SHA" != "$$computed_sha" ]; then
    echo "Wrong sha for canton enterprise jar." >&2
    echo "Trying to download from: $$url" >&2
    echo "Got: $$computed_sha" >&2
    echo "Expected: $$CANTON_ENTERPRISE_SHA" >&2
    exit 1
fi

tar xzf $$tmp

cp **/lib/*.jar $@
""".format(auth = artif_auth),
    tags = ["canton-ee"],
    tools = [
        "@curl_dev_env//:bin/curl",
    ],
    visibility = ["//visibility:public"],
)
