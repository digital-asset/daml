-- Copyright (c) 2020 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0
{-# LANGUAGE CPP #-}
{-# LANGUAGE AllowAmbiguousTypes #-}

module Daml.Trigger.Internal
  ( ACS (..)
  , TriggerA (..)
  , TriggerStateA (..)
  , ActionTriggerUpdate (..)
  , addCommands
  , insertTpl
  , groupActiveContracts
  , deleteTpl
  , lookupTpl
  , applyEvent
  , applyTransaction
  , runRule
  , liftTriggerRule
  , TriggerAState (..)
  , TriggerState (..)
  ) where

import DA.Action.State
import DA.Next.Map (Map)
import qualified DA.Next.Map as Map
#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
import qualified DA.Map as GMap
import DA.Optional (fromOptional)
#endif

import Daml.Trigger.LowLevel hiding (Trigger)

-- public API

-- | Active contract set, you can use `getContracts` to access the templates of
-- a given type.

-- This will change to a Map once we have proper maps in DAML-LF
data ACS = ACS
  { activeContracts :
#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
      GMap.Map TemplateTypeRep
        (GMap.Map AnyContractId AnyTemplate)
#else
      [(AnyContractId, AnyTemplate)]
#endif
  , pendingContracts : Map CommandId [AnyContractId]
  }

-- | TriggerA is the type used in the `rule` of a DAML trigger.
-- Its main feature is that you can call `emitCommands` to
-- send commands to the ledger.
newtype TriggerA a = TriggerA { runTriggerA : ACS -> TriggerRule TriggerAState a }

instance Functor TriggerA where
  fmap f (TriggerA r) = TriggerA $ rliftFmap fmap f r

instance Applicative TriggerA where
  pure = TriggerA . rliftPure pure
  TriggerA ff <*> TriggerA fa = TriggerA $ rliftAp (<*>) ff fa

instance Action TriggerA where
  TriggerA fa >>= f = TriggerA $ rliftBind (>>=) fa (runTriggerA . f)

instance HasTime TriggerA where
  getTime = TriggerA $ const getTime

-- | TriggerStateA is the type used in the `updateState` of a DAML
-- trigger.  It has similar actions in common with `TriggerA`, but
-- cannot use `emitCommands` or `getTime`.
newtype TriggerStateA s a = TriggerStateA { runTriggerStateA : ACS -> State s a }

instance Functor (TriggerStateA s) where
  fmap f (TriggerStateA r) = TriggerStateA $ rliftFmap fmap f r

instance Applicative (TriggerStateA s) where
  pure = TriggerStateA . rliftPure pure
  TriggerStateA ff <*> TriggerStateA fa = TriggerStateA $ rliftAp (<*>) ff fa

instance Action (TriggerStateA s) where
  TriggerStateA fa >>= f = TriggerStateA $ rliftBind (>>=) fa (runTriggerStateA . f)

instance ActionState s (TriggerStateA s) where
  get = TriggerStateA $ const get
  put = TriggerStateA . const . put
  modify = TriggerStateA . const . modify

-- | Features possible in both `updateState` and `rule`.
class ActionTriggerUpdate m where
  -- | Extract the contracts of a given template from the ACS.
  query : Template a => m [(ContractId a, a)]

-- Internal API

addCommands : Map CommandId [Command] -> Commands -> Map CommandId [Command]
addCommands m (Commands cid cmds) = Map.insert cid cmds m

insertTpl : AnyContractId -> AnyTemplate -> ACS -> ACS
#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
insertTpl cid tpl acs = acs { activeContracts = GMap.alter addct cid.templateId acs.activeContracts }
  where addct = Some . GMap.insert cid tpl . fromOptional mempty
#else
insertTpl cid tpl acs = acs { activeContracts = (cid, tpl) :: acs.activeContracts }
#endif

#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
groupActiveContracts :
  [(AnyContractId, AnyTemplate)] -> GMap.Map TemplateTypeRep (GMap.Map AnyContractId AnyTemplate)
groupActiveContracts = foldr (\v@(cid, _) -> GMap.alter (addct v) cid.templateId) GMap.empty
  where addct (cid, tpl) = Some . GMap.insert cid tpl . fromOptional mempty
#else
groupActiveContracts : forall a. a -> a
groupActiveContracts a = a
#endif

deleteTpl : AnyContractId -> ACS -> ACS
#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
deleteTpl cid acs = acs { activeContracts = GMap.alter rmct cid.templateId acs.activeContracts }
  where rmct om = do
          m <- om
          let m' = GMap.delete cid m
          if GMap.null m' then None else Some m'
#else
deleteTpl cid acs = acs { activeContracts = filter (\(cid', _) -> cid /= cid') acs.activeContracts }
#endif

lookupTpl : Template a => AnyContractId -> ACS -> Optional a
lookupTpl cid acs = do
#ifdef DAML_GENMAP && DAML_GENERIC_COMPARISON
  tpl <- GMap.lookup cid =<< GMap.lookup cid.templateId acs.activeContracts
#else
  (_, tpl) <- find ((cid ==) . fst) acs.activeContracts
#endif
  fromAnyTemplate tpl

applyEvent : Event -> ACS -> ACS
applyEvent ev acs = case ev of
  CreatedEvent (Created _ cid tpl) -> insertTpl cid tpl acs
  ArchivedEvent (Archived _ cid) -> deleteTpl cid acs

applyTransaction : Transaction -> ACS -> ACS
applyTransaction (Transaction _ _ evs) acs = foldl (flip applyEvent) acs evs

runRule
  : (Party -> s -> TriggerA ())
  -> TriggerRule (TriggerState s) ()
runRule rule = do
  state <- get
  TriggerRule . zoom zoomIn zoomOut . runTriggerRule . flip runTriggerA state.acs
      $ rule state.party state.userState
  where zoomIn state = TriggerAState state.commandsInFlight state.acs.pendingContracts
        zoomOut state aState =
          let commandsInFlight = aState.commandsInFlight
              acs = state.acs { pendingContracts = aState.pendingContracts }
          in state { commandsInFlight, acs }

liftTriggerRule : TriggerRule TriggerAState a -> TriggerA a
liftTriggerRule = TriggerA . const

data TriggerAState = TriggerAState
  { commandsInFlight : Map CommandId [Command]
  -- ^ Zoomed from TriggerState; used for dedupCreateCmd/dedupExerciseCmd
  -- helpers and extended by emitCommands.
  , pendingContracts : Map CommandId [AnyContractId]
  -- ^ Map from command ids to the contract ids marked pending by that command;
  -- zoomed from TriggerState's acs.
  }

data TriggerState s = TriggerState
  { acs : ACS
  , party : Party
  , userState : s
  , commandsInFlight : Map CommandId [Command]
  }

rliftFmap : ((a -> b) -> f a -> f b) -> (a -> b) -> (r -> f a) -> r -> f b
rliftFmap ub f r = ub f . r

rliftPure : (a -> f a) -> a -> r -> f a
rliftPure ub = const . ub

rliftAp : (f (a -> b) -> f a -> f b) -> (r -> f (a -> b)) -> (r -> f a) -> r -> f b
rliftAp ub ff fa r = ff r `ub` fa r

rliftBind : (f a -> (a -> f b) -> f b) -> (r -> f a) -> (a -> r -> f b) -> r -> f b
rliftBind ub fa f r = fa r `ub` \a -> f a r
