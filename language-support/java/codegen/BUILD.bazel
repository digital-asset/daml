# Copyright (c) 2020 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
    "da_scala_test",
    "scala_source_jar",
    "scaladoc_jar",
)
load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)
load(
    ":codegen.bzl",
    "dar_to_java",
    "mangle_for_java",
)
load(
    "//daml-lf/language:daml-lf.bzl",
    "lf_latest_version",
    "lf_stable_version",
)
load(
    "@com_github_johnynek_bazel_jar_jar//:jar_jar.bzl",
    "jar_jar",
)
load("//bazel_tools:pom_file.bzl", "pom_file")
load("@os_info//:os_info.bzl", "is_windows")

da_scala_binary(
    name = "codegen",
    main_class = "com.daml.lf.codegen.StandaloneMain",
    visibility = ["//visibility:public"],
    deps = [":lib"],
)

da_scala_library(
    name = "lib",
    srcs = glob(["src/main/**/*.scala"]),
    resources = [
        "src/main/resources/logback.xml",
    ],
    tags = ["maven_coordinates=com.daml:codegen-java-lib:__VERSION__"],
    visibility = ["//visibility:public"],
    deps = [
        "//daml-lf/archive:daml_lf_archive_reader",
        "//daml-lf/archive:daml_lf_dev_archive_java_proto",
        "//daml-lf/data",
        "//daml-lf/interface",
        "//language-support/codegen-common",
        "//language-support/java/bindings:bindings-java",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_github_scopt_scopt_2_12",
        "@maven//:com_google_protobuf_protobuf_java",
        "@maven//:com_squareup_javapoet",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_12",
        "@maven//:org_scalaz_scalaz_core_2_12",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

########################################################
####  Unit Tests
########################################################
da_scala_test(
    name = "test",
    srcs = glob(["src/test/**/*.scala"]),
    data = [":test-daml.dar"],
    resource_strip_prefix = "language-support/java/codegen/src/test/resources/",
    resources = glob(["src/test/resources/**/*"]),
    deps = [
        ":lib",
        "//bazel_tools/runfiles:scala_runfiles",
        "//daml-lf/archive:daml_lf_archive_reader",
        "//daml-lf/archive:daml_lf_dev_archive_java_proto",
        "//daml-lf/data",
        "//daml-lf/interface",
        "//language-support/codegen-common",
        "//language-support/java/bindings:bindings-java",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:com_google_protobuf_protobuf_java",
        "@maven//:com_squareup_javapoet",
        "@maven//:com_typesafe_scala_logging_scala_logging_2_12",
        "@maven//:org_scalatest_scalatest_2_12",
        "@maven//:org_slf4j_slf4j_api",
    ],
)

daml_compile(
    name = "test-daml",
    srcs = ["src/test/daml/Foo.daml"],
)

jar_jar(
    name = "shaded_binary",
    input_jar = "//language-support/java/codegen:codegen_deploy.jar",
    rules = "shade_rule",
    tags = ["maven_coordinates=com.daml:codegen-java:__VERSION__"],
    visibility = ["//visibility:public"],
)

pom_file(
    name = "shaded_binary_pom",
    target = ":shaded_binary",
    visibility = ["//visibility:public"],
)

# Create empty Scaladoc JAR for uploading to Maven Central
scaladoc_jar(
    name = "shaded_binary_scaladoc",
    srcs = [],
    tags = ["scaladoc"],
    deps = [],
) if is_windows == False else None

# Create empty Sources JAR for uploading to Maven Central
scala_source_jar(
    name = "shaded_binary_src",
    srcs = [],
)

test_daml_lf_target_versions = [
    "1.0",
    "1.1",
    "1.3",
    "1.6",
    "1.7",
    "1.8",
    "1.dev",
]

[
    [
        dar_to_java(
            name = "test-model-%s" % target,
            src = "//daml-lf/encoder:testing-dar-%s" % target,
            package_prefix = "test",
        ),
        java_test(
            name = "tests-%s" % target,
            srcs = glob([
                "src/test/java/**/*ForAll*.java",
                "src/test/java/**/*For%s*.java" % mangle_for_java(target),
            ]),
            test_class = "com.daml.testing.AllTestsFor%s" % mangle_for_java(target),
            deps = [
                ":test-model-%s.jar" % target,
                "//language-support/java/bindings:bindings-java",
                "@maven//:com_google_protobuf_protobuf_java",
                "@maven//:org_junit_jupiter_junit_jupiter_api",
                "@maven//:org_junit_jupiter_junit_jupiter_engine",
                "@maven//:org_junit_platform_junit_platform_runner",
            ],
        ),
    ]
    for target in test_daml_lf_target_versions
]

########################################################
####  Integration Tests
########################################################

it_daml_lf_target_versions = [
    "1.0",
    "1.1",
    "1.5",
    "latest",
]

# This file was created using a patched version of 0.12.12.
# See https://github.com/digital-asset/daml/pull/2443 for more details
alias(
    name = "integration-tests-model-1.0.dar",
    actual = ":src/it/dar/integration-tests-model-1.0.dar",
)

# This file was created using
# DAML_SDK_VERSION=0.12.20 daml damlc package src/it/daml/Lib.daml integration-tests-model-1.1 --target=1.1 --output src/it/dar/integration-tests-model-1.1.dar
alias(
    name = "integration-tests-model-1.1.dar",
    actual = ":src/it/dar/integration-tests-model-1.1.dar",
)

# This file was created using
# DAML_SDK_VERSION=0.13.10 daml damlc package src/it/daml/Lib.daml integration-tests-model-1.5 --target=1.5 --output src/it/dar/integration-tests-model-1.5.dar
alias(
    name = "integration-tests-model-1.5.dar",
    actual = ":src/it/dar/integration-tests-model-1.5.dar",
)

daml_compile(
    name = "integration-tests-model-latest",
    srcs = glob(["src/it/daml/**/*.daml"]),
    target = lf_latest_version,
)

[
    #
    # Compile the DAR files with and without a package to allow
    # DAML-LF version agnostic tests to be run using generated
    # Java classes with the same qualified name, while the DAML-LF
    # specific tests run using Java classes generated with a
    # package name qualified by the DAML-LF version.
    #
    [
        dar_to_java(
            name = "integration-tests-model-noprefix-%s" % target,
            src = ":integration-tests-model-%s.dar" % target,
        ),
        dar_to_java(
            name = "integration-tests-model-%s" % target,
            src = ":integration-tests-model-%s.dar" % target,
            package_prefix = "lf_%s" % mangle_for_java(target),
        ),
        java_test(
            name = "integration-tests-%s" % target,
            srcs = glob([
                "src/it/java-%s/**/*.java" % target,
                "src/it/java/**/*.java",
            ]),
            test_class = "com.daml.lf_%s.AllTests" % mangle_for_java(target),
            deps = [
                ":integration-tests-model-%s.jar" % target,
                ":integration-tests-model-noprefix-%s.jar" % target,
                "//language-support/java/bindings:bindings-java",
                "//ledger/sandbox:sandbox-scala-tests-lib",
                "@maven//:com_google_protobuf_protobuf_java",
                "@maven//:org_junit_jupiter_junit_jupiter_api",
                "@maven//:org_junit_jupiter_junit_jupiter_engine",
                "@maven//:org_junit_platform_junit_platform_commons",
                "@maven//:org_junit_platform_junit_platform_runner",
            ],
        ),
    ]
    for target in it_daml_lf_target_versions
]

########################################################
####  Ledger Tests
########################################################

daml_compile(
    name = "ledger-tests-model",
    srcs = glob(["src/ledger-tests/daml/**/*.daml"]),
    target = lf_stable_version,
)

dar_to_java(
    name = "ledger-tests-model",
    src = ":ledger-tests-model.dar",
)

da_scala_test(
    name = "ledger-tests",
    srcs = glob(["src/ledger-tests/scala/**/*.scala"]),
    data = [
        ":ledger-tests-model.dar",
    ],
    resource_strip_prefix = "language-support/java/codegen/src/ledger-tests/resources/",
    resources = ["src/ledger-tests/resources/logback-test.xml"],
    tags = ["exclusive"],
    deps = [
        ":ledger-tests-model.jar",
        "//bazel_tools/runfiles:scala_runfiles",
        "//daml-lf/data",
        "//language-support/java/bindings:bindings-java",
        "//ledger/ledger-api-common",
        "//ledger/ledger-api-domain",
        "//ledger/participant-state",
        "//ledger/sandbox",
        "//ledger/sandbox:sandbox-scala-tests-lib",
        "//libs-scala/ports",
        "//libs-scala/resources",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:com_google_protobuf_protobuf_java",
        "@maven//:io_grpc_grpc_api",
        "@maven//:io_grpc_grpc_stub",
        "@maven//:io_reactivex_rxjava2_rxjava",
        "@maven//:org_scalatest_scalatest_2_12",
        "@maven//:org_scalaz_scalaz_core_2_12",
    ],
)

exports_files(["src/main/resources/logback.xml"])
