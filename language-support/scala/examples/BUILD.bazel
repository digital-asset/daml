# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
    "silencer_plugin",
)
load("//rules_daml:daml.bzl", "daml_compile")
load("//language-support/scala/codegen:codegen.bzl", "dar_to_scala")
load("@scala_version//:index.bzl", "scala_version")

filegroup(
    name = "quickstart-scala-src",
    srcs = glob(
        ["quickstart-scala/*"],
        exclude = [
            "**/.*",
            "**/target",
        ],
        exclude_directories = 0,
    ),
)

genrule(
    name = "quickstart-scala",
    srcs = [
        ":quickstart-scala-src",
        "//docs:daml-assistant-iou-setup",
    ],
    outs = ["quickstart-scala.tar"],
    cmd = """
        DIR=$$(mktemp -d)
        mkdir $$DIR/quickstart-scala
        trap "rm -rf $$DIR" EXIT
        cp -rL $(SRCS) $$DIR/quickstart-scala/
        sed -i 's/SCALA_VERSION/{scala_version}/' $$DIR/quickstart-scala/build.sbt
        rm -rf $@/target
        rm -rf $@/project/target
        rm -rf $@/application/target
        rm -rf $@/scala-codegen/target
        $(execpath //bazel_tools/sh:mktar) $@ -C $$DIR quickstart-scala
    """.format(scala_version = scala_version),
    tools = [
        ":quickstart-scala-bin",
        "//bazel_tools/sh:mktar",
    ],  # this is to make sure that quickstart-scala compiles
    visibility = ["//visibility:public"],
)

dar_to_scala(
    name = "quickstart-scala-codegen",
    srcs = ["//docs:quickstart-model.dar"],
    package_prefix = "com.daml.quickstart.iou.model",
    srcjar_out = "quickstart-scala-codegen.srcjar",
)

da_scala_library(
    name = "quickstart-scala-codegen-lib",
    srcs = [":quickstart-scala-codegen.srcjar"],
    plugins = [
        silencer_plugin,
    ],
    scalacopts = [
        "-P:silencer:lineContentFilters=import _root_.scala.language.higherKinds;",
    ],
    deps = ["//language-support/scala/bindings"],
)

da_scala_binary(
    name = "quickstart-scala-bin",
    srcs = glob(["quickstart-scala/application/src/main/scala/**/*.scala"]),
    main_class = "com.daml.quickstart.iou.IouMain",
    resources = glob(["quickstart-scala/application/src/main/resources/**/*"]),
    scala_deps = [
        "@maven//:org_scala_lang_modules_scala_collection_compat",
    ],
    deps = [
        ":quickstart-scala-codegen-lib",
        "//language-support/scala/bindings",
        "//language-support/scala/bindings-akka",
        "//ledger-api/rs-grpc-bridge",
    ],
)

da_scala_binary(
    name = "iou-no-codegen-bin",
    srcs = glob(["iou-no-codegen/application/src/main/scala/**/*.scala"]),
    main_class = "com.daml.quickstart.iou.IouMain",
    resources = glob(["iou-no-codegen/application/src/main/resources/**/*"]),
    deps = [
        "//language-support/scala/bindings",
        "//language-support/scala/bindings-akka",
        "//ledger-api/rs-grpc-bridge",
    ],
)
