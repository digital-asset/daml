# Copyright (c) 2020 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("//rules_daml:daml.bzl", "daml_compile")
load("//bazel_tools:haskell.bzl", "da_haskell_test")
load("@build_environment//:configuration.bzl", "sdk_version")
load("@os_info//:os_info.bzl", "is_windows")

# This rule builds a dar from the daml sources under the 'daml'
# directory. It is referenced from the 'build-and-lint' test that
# follows.
daml_compile(
    name = "build-and-lint-1.0.0",
    srcs = glob(["daml/**/*.daml"]),
    main_src = "daml/Main.daml",
)

# build-and-lint
# ==============
#
# 'build-and-lint.sh' is an integration test. It builds a DAR from
#  some DAML, invokes daml2ts on the DAR, uploads the DAR to a ledger
#  and then runs tests on the TypeScript generated by daml2ts via a
#  json-api connected to the ledger.
#
#  The main inputs to 'build-and-lint' are:
#     - Bazel artifacts : '@daml/types' and '@daml/ledger' (these are
#       npm packages);
#     - Source artifacts:
#       - 'ts/codegen/tests/daml/*'
#         The DAML that goes into the DAR ('build-and-lint-1.0.0.dar');
#       - 'ts/codegen/tests/ts/*'
#         A skeleton yarn workspaces root containing a TypeScript package
#         'build-and-lint' which houses the test driver 'test.ts'.
#
# 'build-and-lint' creates a temp directory ('root' say), and uses the
# above inputs (via filesystem copies, damlc, daml2ts and yarn install)
# to produce a filesystem with the following structure:
#
#   root/
#      build-and-lint/
#        src/
#          __tests__/
#            test.ts
#      daml2ts/
#        build-and-lint-1.0.0/  -- The TypeScript generated from 'build-and-lint-1.0.0.dar'
#        057eed/
#        ...
#      package.json  -- Enumeration of the workspaces : build-and-lint, daml2ts/build-and-lint-1.0.0, ...
#      node_modules/
#        @daml/
#          types/
#          ledger/
#        ...
#
# In more detail, the actions of 'build-and-lint' are to:
#  - Invoke 'daml2ts' on 'build-and-lint-1.0.0.dar' produce TypeScript bindings (
#    in the 'daml2ts' directory);
#  - Invoke from 'root':
#    - 'yarn workspaces run install';
#    - 'yarn workspaces run build';
#    - 'yarn workspaces run lint';
#  - Finally, invoke 'yarn test' from the 'root/build-and-lint' directory.
#
# That last step causes 'build-and-lint/src/__tests__/test.ts' to be executed which:
#  - Spins up a sandbox running the 'build-and-lint-1.0.0.dar';
#  - Spins up a http-json-api connected to the sandbox;
#  - Evaluates assertions of http-json-api specified ledger operations
#    involving contracts defined by the dar;
#  - Gracefully tears down the processes it started when its work is done.
# All in all, a pretty slick bit of work!
sh_test(
    name = "build-and-lint",
    size = "large",
    srcs = ["build-and-lint.sh"],
    args = [
        "$(location //:java)",
        "$(location //:yarn)",
        "$(location //:daml2ts)",
        "$(location //ledger/sandbox:sandbox-binary_deploy.jar)",
        "$(location //ledger-service/http-json:http-json-binary_deploy.jar)",
        "$(location :build-and-lint-1.0.0.dar)",
        "$(location ts/package.json)",
        "$(location //language-support/ts/daml-types:npm_package)",
        "$(location //language-support/ts/daml-ledger:npm_package)",
        sdk_version,
    ],
    data = [
        "//:java",
        "//:yarn",
        "//:daml2ts",
        "//ledger/sandbox:sandbox-binary_deploy.jar",
        "//ledger-service/http-json:http-json-binary_deploy.jar",
        ":build-and-lint-1.0.0.dar",
        "//language-support/ts/daml-types:npm_package",
        "//language-support/ts/daml-ledger:npm_package",
    ] + glob(
        ["ts/**"],
        exclude = [
            "ts/**/node_modules/**",
            "ts/**/lib/**",
        ],
    ),
    tags = [
        "exclusive",  # Due to the use of hardcoded ports in 'build-and-lint.sh'.
    ],
    deps = [
        "@bazel_tools//tools/bash/runfiles",
    ],
) if not is_windows else None

# daml2ts
# =======
#
# This target is a unit-test. It does similar things to 'build-and-test'
# but generates all its inputs as it needs to and stops short of spinning
# up ledgers and/or executing any TypeScript.

da_haskell_test(
    name = "daml2ts",
    size = "large",
    srcs = ["src/DA/Test/Daml2Ts.hs"],
    args = [
        "$(location //:yarn)",
        "$(location //language-support/ts/daml-types:npm_package)",
    ],
    data = [
        "//:yarn",
        "//compiler/damlc",
        "//language-support/ts/codegen:daml2ts",
        "//language-support/ts/daml-types:npm_package",
        "@davl//:released/davl-upgrade-v3-v4.dar",
        "@davl//:released/davl-upgrade-v4-v5.dar",
        "@davl//:released/davl-v3.dar",
        "@davl//:released/davl-v4.dar",
        "@davl//:released/davl-v5.dar",
    ],
    hackage_deps = [
        "base",
        "extra",
        "filepath",
        "process",
        "tasty",
        "tasty-hunit",
    ],
    main_function = "DA.Test.Daml2Ts.main",
    src_strip_prefix = "src",
    visibility = ["//visibility:public"],
    deps = [
        "//libs-haskell/bazel-runfiles",
        "//libs-haskell/da-hs-base",
    ],
) if not is_windows else None
