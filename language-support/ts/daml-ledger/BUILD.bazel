# Copyright (c) 2020 The DAML Authors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("@os_info//:os_info.bzl", "is_windows")
load("@build_bazel_rules_nodejs//:index.bzl", "pkg_npm")
load("@language_support_ts_deps//typescript:index.bzl", "tsc")
load("@npm_bazel_typescript//:index.bzl", "ts_library")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("//language-support/ts:eslint.bzl", "eslint_test")
load("@sdk_version//:sdk_version.bzl", "sdk_version")

ts_library(
    name = "daml-ledger",
    srcs = glob(["**/*.ts"]),
    data = [
        ":LICENSE",
        ":README.md",
        ":package.json",
    ],
    module_name = "@daml/ledger",
    node_modules = "@language_support_ts_deps//:node_modules",
    tsconfig = ":tsconfig.json",
    visibility = ["//visibility:public"],
    deps = [
        "//language-support/ts/daml-types",
        "@language_support_ts_deps//@mojotech/json-type-validation",
        "@language_support_ts_deps//cross-fetch",
    ],
) if not is_windows else None

genrule(
    name = "license",
    srcs = ["//:LICENSE"],
    outs = ["LICENSE"],
    cmd = """
      cp $(location //:LICENSE) $@
    """,
)

eslint_test(
    name = "lint",
    srcs = glob(
        ["**/*.ts"],
        exclude = ["lib/**/*"],
    ),
)

tsc(
    name = "daml-ledger.commonjs",
    data = ["index.ts"] + [
        "tsconfig.json",
    ] + [
        "//language-support/ts/daml-types",
        "@language_support_ts_deps//@mojotech/json-type-validation",
        "@language_support_ts_deps//cross-fetch",
    ],
    outs = [
        s.replace(".ts", ext)
        for ext in [".js", ".d.ts"]
        for s in ["index.ts"]
    ],
    args = [
        "--outDir",
        "$(RULEDIR)",
        "--project",
        "$(location tsconfig.json)",
        "--declaration",
    ],
)

pkg_npm(
    name = "npm_package",
    srcs = [
        ":package.json",
        ":tsconfig.json",
    ],
    substitutions = {"0.0.0-SDKVERSION": sdk_version},
    visibility = ["//visibility:public"],
    deps = [
        ":daml-ledger.commonjs",
        ":license",
    ],
) if not is_windows else None
