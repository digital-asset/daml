-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE AllowAmbiguousTypes #-}
{-# LANGUAGE InstanceSigs #-}

module Daml.Script.Questions.Commands where

import Daml.Script.Internal
import DA.Bifunctor

data Command
  = Create with
      argC : AnyTemplate
  | Exercise with
      tplId : TemplateTypeRep
      cId : ContractId ()
      argE : AnyChoice
  | ExerciseByKey with
      tplId : TemplateTypeRep
      keyE : AnyContractKey
      argE : AnyChoice
  | CreateAndExercise with
      tplArgCE : AnyTemplate
      choiceArgCE : AnyChoice

-- The ledger api can only return create and exercise results, therefore `Exercise`, `ExerciseByKey` and `CreateByExercise` all return `ExerciseResult`s
data CommandResult
  = CreateResult (ContractId ())
  | ExerciseResult LedgerValue

instance Show CommandResult where
  show CreateResult {} = "CreateResult"
  show ExerciseResult {} = "ExerciseResult"

-- | This is used to build up the commands send as part of `submit`.
-- If you enable the `ApplicativeDo` extension by adding
-- `{-# LANGUAGE ApplicativeDo #-}` at the top of your file, you can
-- use `do`-notation but the individual commands must not depend
-- on each other and the last statement in a `do` block
-- must be of the form ``return expr`` or ``pure expr``.
data Commands a = Commands with
    commands : [Command]
    continue : [CommandResult] -> a
  deriving Functor
  
data Disclosure = Disclosure with
   templateId: TemplateTypeRep
   contractId : ContractId ()
   blob: Text
 deriving (Eq, Ord)

-- Expected number of command results for a given command
expectedCommandResults : Command -> Int
expectedCommandResults Create {} = 1
expectedCommandResults Exercise {} = 1
expectedCommandResults ExerciseByKey {} = 1
-- Create and exercise gives 2 results, so we account for that here
expectedCommandResults CreateAndExercise {} = 2

-- Expected number of command results for a list of commands
expectedCommandsResults : [Command] -> Int
expectedCommandsResults = foldr ((+) . expectedCommandResults) 0

instance Applicative Commands where
  pure : a -> Commands a
  pure x = Commands [] (const x)

  liftA2 : (a -> b -> c) -> Commands a -> Commands b -> Commands c
  liftA2 f a b = Commands
    { commands = a.commands <> b.commands
    , continue = \results -> 
        let (aResults, bResults) = splitAt (expectedCommandsResults a.commands) results
        in f (a.continue aResults) (b.continue bResults)
    }

-- | HIDE A version of 'createCmd' without constraints.
internalCreateCmd : AnyTemplate -> Commands (ContractId ())
internalCreateCmd arg = Commands [Create arg] $ \case
  [CreateResult res] -> res
  res -> error $ "Expected [CreateResult], got " <> show res

-- | HIDE A version of 'exerciseCmd' without constraints.
internalExerciseCmd : TemplateTypeRep -> ContractId () -> AnyChoice -> Commands LedgerValue
internalExerciseCmd tplTypeRep cId arg = Commands [Exercise tplTypeRep cId arg] $ \case
  [ExerciseResult res] -> res
  res -> error $ "Expected [ExerciseResult], got " <> show res

-- | HIDE A version of 'exerciseByKeyCmd' without constraints.
internalExerciseByKeyCmd : TemplateTypeRep -> AnyContractKey -> AnyChoice -> Commands LedgerValue
internalExerciseByKeyCmd tplTypeRep key arg = Commands [ExerciseByKey tplTypeRep key arg] $ \case
  [ExerciseResult res] -> res
  res -> error $ "Expected [ExerciseResult], got " <> show res

-- | HIDE A version of 'createAndExerciseCmd' without constraints.
-- Create and exercise gives a create result and exercise result.
internalCreateAndExerciseCmd : AnyTemplate -> AnyChoice -> Commands (ContractId (), LedgerValue)
internalCreateAndExerciseCmd tplArg choiceArg = Commands [CreateAndExercise tplArg choiceArg] $ \case
  [CreateResult cid, ExerciseResult res] -> (cid, res)
  res -> error $ "Expected [CreateResult, ExerciseResult], got " <> show res

-- | Create a contract of the given template.
createCmd : (Template t, HasAgreement t) => t -> Commands (ContractId t)
-- The 'HasAgreement t' constraint prevents this function from being used on interface types.
createCmd arg = coerceContractId <$> internalCreateCmd (toAnyTemplate arg)

-- | Exercise a choice on the given contract.
exerciseCmd : forall t c r. Choice t c r => ContractId t -> c -> Commands r
exerciseCmd cId arg = fromLedgerValue @r <$> internalExerciseCmd (templateTypeRep @t) (coerceContractId cId) (toAnyChoice @t arg)

-- | Exercise a choice on the contract with the given key.
exerciseByKeyCmd : forall t k c r. (TemplateKey t k, Choice t c r) => k -> c -> Commands r
exerciseByKeyCmd key arg = fromLedgerValue @r <$> internalExerciseByKeyCmd (templateTypeRep @t) (toAnyContractKey @t key) (toAnyChoice @t arg)

-- | Create a contract and exercise a choice on it in the same transaction, returns the created ContractId, and the choice result.
createAndExerciseCmdWithCid : forall t c r. (Template t, Choice t c r, HasAgreement t) => t -> c -> Commands (ContractId t, r)
-- The 'HasAgreement t' constraint prevents this function from being used on interface types.
createAndExerciseCmdWithCid tplArg choiceArg = bimap coerceContractId (fromLedgerValue @r) <$> internalCreateAndExerciseCmd (toAnyTemplate tplArg) (toAnyChoice @t choiceArg)

-- | Create a contract and exercise a choice on it in the same transaction, returns only the choice result.
createAndExerciseCmd : forall t c r. (Template t, Choice t c r, HasAgreement t) => t -> c -> Commands r
createAndExerciseCmd tplArg choiceArg = snd <$> createAndExerciseCmdWithCid tplArg choiceArg

-- | Archive the given contract.
--
-- `archiveCmd cid` is equivalent to `exerciseCmd cid Archive`.
archiveCmd : Choice t Archive () => ContractId t -> Commands ()
archiveCmd cid = exerciseCmd cid Archive
