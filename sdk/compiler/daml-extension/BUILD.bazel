# Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("@build_environment//:configuration.bzl", "npm_version")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "json-files",
    srcs = glob([
        "*.json",
        "syntaxes/*.json",
    ]),
)

sh_test(
    name = "valid-json",
    srcs = ["ci-tests.sh"],
    args = [
        "$(location @jq_dev_env//:jq)",
        "$(locations :json-files)",
    ],
    data = [
        ":json-files",
        "@jq_dev_env//:jq",
    ],
    # This is required to get the test to run on Windows. Otherwise, it looks
    # like Bazel decides that because it cannot create symlinks on Windows, it
    # may as well just give up and start the test without any of its
    # dependencies available at all. But not warn about it, so the script can
    # just fail at build time when it doesn't find anything.
    deps = ["@bazel_tools//tools/bash/runfiles"],
)

# For some reason,
# 1. Bazel only exposes the node_modules dependency as a list of files, not as
#    a folder, and
# 2. Copying these files over is surprisingly slow on my machine.
#
# Because `vsce` needs to run in a folder where all of the node_modules
# dependencies are already populated, this separate step takes all of the
# node_module files, one by one (because that is how Bazel exposes them),
# copies them to their intended place, and then bundles the whole node_modules
# folder as a tarball so the next task, below, can depend on that cached
# tarball and be fast.
# Also for some reason on Windows I get "cannot ceate node_modules: file
# exists", so at this point I'm completely out of trust.
genrule(
    name = "node_deps_cache",
    srcs = [
        "//compiler/daml-extension:package.json",
        "//compiler/daml-extension:yarn.lock",
    ],
    outs = ["node_modules.tar.gz"],
    cmd = """
        set -euo pipefail

        YARN=$$PWD/$(execpath //:yarn)
        MKTGZ=$(execpath //bazel_tools/sh:mktgz)

        PACKAGE_DIR=$$(dirname $(location //compiler/daml-extension:package.json))

        cd $$PACKAGE_DIR
        $$YARN install \
            --no-cache \
            --frozen-lockfile \
            --production=false \
            --ignore-optional

        cd $$OLDPWD
        $$MKTGZ $@ --dereference -C $$PACKAGE_DIR node_modules
    """,
    tools = [
        "//:yarn",
        "//bazel_tools/sh:mktgz",
    ],
)

genrule(
    name = "webview-stylesheet",
    srcs = ["src/webview.css"],
    outs = ["webview-stylesheet.css"],
    cmd = """
    cp "$(location src/webview.css)" "$@"
    """,
)

# This rule is not reproducible. `vsce package` generates a `.vsix` file which
# is just a zip archive. The order of entries in that archive is
# non-deterministic and the individual entries contain non-reproducible
# timestamps.
genrule(
    name = "vsix",
    srcs = glob([
        "package.json",
        "syntaxes/*",
        "snippets/*",
        "images/*",
        "*.json",
        "README.md",
        "yarn.lock",
        ".vscodeignore",
        "src/**",
    ]) + [
        ":node_deps_cache",
    ],
    outs = ["daml-bundled.vsix"],
    cmd = """
        set -euo pipefail

        export HOME=/does-not-exist

        # #
        # Create temporary dir to edit files (`src` files are readonly),
        #   then define function to remove temporary dir,
        #   which will be executed when cmd will try to exit the `cmd`.
        TMP_DIR=$$(mktemp -d)
        cleanup() {{ rm -rf $$TMP_DIR || return; }}
        trap cleanup EXIT

        DIR=$$PWD
        YARN=$$DIR/$(location //:yarn)
        YARN_DIR=$$(dirname $$DIR/$(location //:yarn))
        VSCE=$$DIR/$(location @daml_extension_deps//@vscode/vsce/bin:vsce)

        cp -r compiler/daml-extension $$TMP_DIR
        cd $$TMP_DIR/daml-extension

        echo $(TARGET_CPU) > ../target_cpu
        tar xzf $$DIR/$(location :node_deps_cache)

        sed -i "s/__VERSION__/{npm}/" package.json
        sed -i 's/"name": "daml"/"name": "daml-bundled"/' package.json

        $$YARN compile
        PATH=$$YARN_DIR:$$PATH $$VSCE package --yarn -o $$DIR/$@
    """.format(npm = npm_version),
    tools = [
        "//:yarn",
        "@daml_extension_deps//@vscode/vsce/bin:vsce",
    ],
)
