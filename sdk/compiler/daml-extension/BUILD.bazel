# Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

package(default_visibility = ["//visibility:public"])

load("@build_environment//:configuration.bzl", "npm_version")

filegroup(
    name = "json-files",
    srcs = glob([
        "*.json",
        "syntaxes/*.json",
    ]),
)

sh_test(
    name = "valid-json",
    srcs = ["ci-tests.sh"],
    args = [
        "$(location @jq_dev_env//:jq)",
        "$(locations :json-files)",
    ],
    data = [
        ":json-files",
        "@jq_dev_env//:jq",
    ],
    # This is required to get the test to run on Windows. Otherwise, it looks
    # like Bazel decides that because it cannot create symlinks on Windows, it
    # may as well just give up and start the test without any of its
    # dependencies available at all. But not warn about it, so the script can
    # just fail at build time when it doesn't find anything.
    deps = ["@bazel_tools//tools/bash/runfiles"],
)

# For some reason,
# 1. Bazel only exposes the node_modules dependency as a list of files, not as
#    a folder, and
# 2. Copying these files over is surprisingly slow on my machine.
#
# Because `vsce` needs to run in a folder where all of the node_modules
# dependencies are already populated, this separate step takes all of the
# node_module files, one by one (because that is how Bazel exposes them),
# copies them to their intended place, and then bundles the whole node_modules
# folder as a tarball so the next task, below, can depend on that cached
# tarball and be fast.
# Also for some reason on Windows I get "cannot ceate node_modules: file
# exists", so at this point I'm completely out of trust.
genrule(
    name = "node_deps_cache",
    srcs = [
        "@daml_extension_deps//:node_modules",
        "//compiler/daml-extension:yarn.lock",
    ],
    outs = ["node_modules.tar.gz"],
    cmd = """
        if [[ -d node_modules ]]; then
            rm -rf node_modules
        fi
        mkdir node_modules
        cd node_modules
        for f in $(locations @daml_extension_deps//:node_modules); do
            # exclude @bazel and @types directories that will be copied later
            if [[ $$f != *"@"* ]]; then
              cp -r ../$$f ./
            fi
        done
        cp -r ../$(BINDIR)/external/daml_extension_deps/node_modules/@types ./
        cp -r ../$(BINDIR)/external/daml_extension_deps/node_modules/@bazel ./
        cd ..
        # Avoid file path too long errors on Windows of the form
        #   .../tar_dev_env/usr/bin/tar: node_modules/.cache/terser-webpack-plugin/...: file name is too long (cannot be split); not dumped
        $(execpath //bazel_tools/sh:mktgz) $@ node_modules
    """,
    tools = ["//bazel_tools/sh:mktgz"],
)

genrule(
    name = "webview-stylesheet",
    srcs = ["src/webview.css"],
    outs = ["webview-stylesheet.css"],
    cmd = """
    cp "$(location src/webview.css)" "$@"
    """,
)

# This rule is not reproducible. `vsce package` generates a `.vsix` file which
# is just a zip archive. The order of entries in that archive is
# non-deterministic and the individual entries contain non-reproducible
# timestamps.
genrule(
    name = "vsix",
    srcs = glob([
        "package.json",
        "syntaxes/*",
        "snippets/*",
        "images/*",
        "*.json",
        "README.md",
        "yarn.lock",
        ".vscodeignore",
        "src/*",
    ]) + [
        ":node_deps_cache",
    ],
    outs = ["daml-bundled.vsix"],
    # rm -rf can fail with "directory not empty" on Windows.
    # As a workaround we add `|| return`.
    cmd = """
        export HOME=/does-not-exist
        set -euo pipefail
        TMP_DIR=$$(mktemp -d)
        cleanup () {{ rm -rf $$TMP_DIR || return; }}
        trap cleanup EXIT
        DIR=$$PWD
        cp -r compiler/daml-extension $$TMP_DIR
        # we store the target cpu value to ensure the archive is platform specific
        echo $(TARGET_CPU) > $$TMP_DIR/target_cpu
        cd $$TMP_DIR/daml-extension
        tar xzf $$DIR/$(location :node_deps_cache)
        sed -i "s/__VERSION__/{npm}/" package.json
        sed -i 's/"name": "daml"/"name": "daml-bundled"/' package.json
        $$DIR/$(location //:yarn) compile
        # vsce needs Yarn or NPM to get the minimal dependency set to include in the artifact
        # Using Yarn has a bug whereby some transitive dependencies are not included (specifically concat-map)
        # NPM can't find npm-cli.js for some reason and won't run
        # So for now, we explicitly depend on concat-map in `package.json` to avoid this
        PATH=$$(dirname $$DIR/$(location //:yarn)):$$PATH $$DIR/$(location @daml_extension_deps//@vscode/vsce/bin:vsce) package --yarn -o $$DIR/$@
    """.format(npm = npm_version),
    tools = [
        "//:yarn",
        "@daml_extension_deps//@vscode/vsce/bin:vsce",
    ],
)
