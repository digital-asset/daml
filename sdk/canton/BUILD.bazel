# Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@io_bazel_rules_scala//scala:scala.bzl", "scala_binary")
load("//bazel_tools:proto.bzl", "proto_gen", "proto_jars")
load("//bazel_tools/packaging:packaging.bzl", "package_oci_component")
load("//canton:canton_version.bzl", "CANTON_OPEN_SOURCE_SHA", "CANTON_OPEN_SOURCE_TAG")

genrule(
    name = "canton_open_source_jar",
    srcs = [],
    outs = ["canton_jar.jar"],
    cmd = """
TMPDIR=$$(mktemp -d)
# oras requires some home directory, we provide a random one
FAKE_HOME=$$(mktemp -d)
export HOME=$$FAKE_HOME
export USERPROFILE=$$FAKE_HOME
REF=europe-docker.pkg.dev/da-images/public-unstable/components/canton-open-source:{tag}@{sha}
$(execpath @dpm_binary//:oras) pull $$REF --output $$TMPDIR
cp $$TMPDIR/lib/canton-open-source-{tag}.jar $@
""".format(
        sha = CANTON_OPEN_SOURCE_SHA,
        tag = CANTON_OPEN_SOURCE_TAG,
    ),
    tools = ["@dpm_binary//:oras"],
    visibility = ["//visibility:private"],
)

java_import(
    name = "community_app-lib",
    jars = [":canton_open_source_jar"],
    visibility = ["//visibility:public"],
)

scala_binary(
    name = "community_app",
    main_class = "com.digitalasset.canton.CantonCommunityApp",
    visibility = ["//visibility:public"],
    deps = [":community_app-lib"],
)

copy_file(
    name = "ledger-api-scala-file-into-proto",
    src = ":community/ledger-api-scala/src/main/protobuf/com/daml/ledger/api/scalapb/package.proto",
    out = "community/ledger-api-proto/src/main/protobuf/com/daml/ledger/api/scalapb/package.proto",
)

proto_jars(
    name = "ledger_api_proto",
    srcs = glob(["community/ledger-api-proto/src/main/protobuf/com/daml/ledger/api/v*/**/*.proto"]),
    grpc = True,
    java_conversions = True,
    java_deps = [
        "@maven//:com_google_api_grpc_proto_google_common_protos",
        "@maven//:com_thesamet_scalapb_scalapb_runtime_2_13",
    ],
    maven_artifact_prefix = "ledger-api",
    maven_artifact_scala_suffix = "scalapb",
    maven_group = "com.daml",
    proto_deps = ["//canton/community/daml-lf/ledger-api-value:ledger_api_value_proto"],
    scalapb_options_files = [
        ":ledger-api-scala-file-into-proto",
    ],
    strip_import_prefix = "community/ledger-api-proto/src/main/protobuf",
    visibility = ["//visibility:public"],
    deps = [
        "@com_google_protobuf//:any_proto",
        "@com_google_protobuf//:descriptor_proto",
        "@com_google_protobuf//:duration_proto",
        "@com_google_protobuf//:empty_proto",
        "@com_google_protobuf//:field_mask_proto",
        "@com_google_protobuf//:struct_proto",
        "@com_google_protobuf//:timestamp_proto",
        "@com_google_protobuf//:wrappers_proto",
        "@go_googleapis//google/rpc:error_details_proto",
        "@go_googleapis//google/rpc:status_proto",
        "@scalapb//:scalapb_proto",
    ],
)

# For DPM!
package_oci_component(
    name = "canton-community-oci",
    component_manifest = ":component.yaml",
    platform_agnostic = True,
    resources = [":community_app_deploy.jar"],
    tags = ["no-cache"],
    visibility = ["//visibility:public"],
)
