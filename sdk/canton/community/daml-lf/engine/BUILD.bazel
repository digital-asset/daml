# Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_library",
    "lf_scalacopts",
    "lf_scalacopts_stricter",
)
load(
    "//canton/community/daml-lf/language:daml-lf.bzl",
    "COMPILER_LF_MAJOR_VERSIONS",
    "DEFAULT_LF_VERSION",
    "DEV_LF_VERSION",
    "mangle_for_damlc",
)
load(
    "//rules_daml:daml.bzl",
    "daml_compile",
)

da_scala_library(
    name = "engine",
    srcs = glob(
        ["src/main/**/*.scala"],
        exclude = ["src/main/**/ScriptEngine.scala"],
    ),
    scala_deps = [
        "@maven//:org_scalaz_scalaz_core",
        "@maven//:org_typelevel_cats_core",
        "@maven//:org_typelevel_cats_kernel",
        "@maven//:org_typelevel_paiges_core",
    ],
    scalacopts = lf_scalacopts_stricter,
    tags = ["maven_coordinates=com.daml:daml-lf-engine:__VERSION__"],
    visibility = ["//visibility:public"],
    deps = [
        "@maven//:com_daml_contextualized_logging_2_13",
        "@maven//:com_daml_daml_lf_archive_2_13",
        "@maven//:com_daml_daml_lf_data_2_13",
        "@maven//:com_daml_daml_lf_interpreter_2_13",
        "@maven//:com_daml_daml_lf_language_2_13",
        "@maven//:com_daml_daml_lf_snapshot_proto_2_13",
        "@maven//:com_daml_daml_lf_stable_packages_2_13",
        "@maven//:com_daml_daml_lf_transaction_2_13",
        "@maven//:com_daml_daml_lf_validation_2_13",
        "@maven//:com_daml_nameof_2_13",
        "@maven//:com_daml_scala_utils_2_13",
        "@maven//:com_google_protobuf_protobuf_java",
    ],
)

da_scala_library(
    name = "script-engine",
    srcs = glob(["src/main/**/ScriptEngine.scala"]),
    scala_deps = [
        "@maven//:org_scalaz_scalaz_core",
    ],
    scalacopts = lf_scalacopts_stricter,
    tags = ["maven_coordinates=com.daml:daml-lf-script-engine:__VERSION__"],
    visibility = ["//visibility:public"],
    deps = [
        "@maven//:com_daml_contextualized_logging_2_13",
        "@maven//:com_daml_daml_lf_data_2_13",
        "@maven//:com_daml_daml_lf_interpreter_2_13",
        "@maven//:com_daml_daml_lf_language_2_13",
        "@maven//:com_daml_daml_lf_transaction_2_13",
    ],
)

ENGINE_TEST_FILES = \
    [
        "BasicTests",
        "AuthTests",
        "Demonstrator",
    ]

[
    daml_compile(
        name = "{name}-v{major}".format(
            name = name,
            major = major,
        ),
        srcs = ["src/test/daml/%s/%s.daml" % (name, name)],
        target = DEFAULT_LF_VERSION,
    )
    for name in ENGINE_TEST_FILES
    for major in COMPILER_LF_MAJOR_VERSIONS
]

[
    daml_compile(
        name = "{name}-{version}".format(
            name = name,
            version = mangle_for_damlc(version),
        ),
        srcs = ["src/test/daml/%s/%s.daml" % (name, name)],
        target = version,
    )
    for name in ENGINE_TEST_FILES
    for version in [DEV_LF_VERSION]
]

[
    daml_compile(
        name = "LargeTransaction-v{}".format(major),
        srcs = ["src/test/daml/LargeTransaction/LargeTransaction.daml"],
        target = DEFAULT_LF_VERSION,
    )
    for major in COMPILER_LF_MAJOR_VERSIONS
]
