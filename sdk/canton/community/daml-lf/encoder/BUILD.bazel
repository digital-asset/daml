# Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
    "da_scala_test_suite",
    "lf_scalacopts",
    "lf_scalacopts_stricter",
)
load(
    "//canton/community/daml-lf/language:daml-lf.bzl",
    "ALL_LF_VERSIONS",
    "ENGINE_LF_VERSIONS",
    "lf_version_is_dev",
    "mangle_for_java",
    "version_in",
)

da_scala_library(
    name = "encoder",
    srcs = glob(
        ["src/main/**/*.scala"],
        exclude = ["src/main/scala/com/digitalasset/daml/lf/archive/testing/DamlLfEncoder.scala"],
    ),
    scala_deps = [
        "@maven//:org_scalaz_scalaz_core",
    ],
    scalacopts = lf_scalacopts_stricter,
    tags = ["maven_coordinates=com.daml:daml-lf-encoder:__VERSION__"],
    visibility = ["//visibility:public"],
    deps = [
        "@maven//:com_daml_crypto_2_13",
        "@maven//:com_daml_daml_lf_archive_2_13",
        "@maven//:com_daml_daml_lf_data_2_13",
        "@maven//:com_daml_daml_lf_language_2_13",
        "@maven//:com_google_protobuf_protobuf_java",
    ],
)

da_scala_binary(
    name = "encoder_binary",
    srcs = glob(["src/main/scala/com/digitalasset/daml/lf/archive/testing/DamlLfEncoder.scala"]),
    main_class = "com.digitalasset.daml.lf.archive.testing.DamlLfEncoder",
    scalacopts = lf_scalacopts_stricter,
    visibility = ["//visibility:public"],
    runtime_deps = [
        "@maven//:ch_qos_logback_logback_classic",
    ],
    deps = [
        ":encoder",
        "@maven//:com_daml_daml_lf_archive_2_13",
        "@maven//:com_daml_daml_lf_archive_encoder_2_13",
        "@maven//:com_daml_daml_lf_data_2_13",
        "@maven//:com_daml_daml_lf_language_2_13",
        "@maven//:com_daml_daml_lf_parser_2_13",
        "@maven//:com_daml_daml_lf_stable_packages_2_13",
        "@maven//:com_daml_daml_lf_validation_2_13",
        "@maven//:com_google_protobuf_protobuf_java",
    ],
)

[
    [
        filegroup(
            name = "lf_%s" % target,
            srcs = glob([
                "src/test/lf/*_all_*.lf",
                "src/test/lf/*_%s_*.lf" % target,
            ]),
        ),
        genrule(
            name = "metadata-%s" % target,
            outs = ["metadata-%s.lf" % target],
            cmd = """echo "metadata ( 'testing-dar-%s' : '0.0.0' )" > $@""" % mangle_for_java(target),
        ),
        genrule(
            name = "testing-dar-%s" % target,
            srcs = [
                ":metadata-%s.lf" % target,  # needs to be first
                ":lf_%s" % target,
            ],
            outs = ["test-%s.dar" % target],
            cmd = "$(location :encoder_binary) $(SRCS) --output $@ --target %s" % target,
            tools = [":encoder_binary"],
            visibility = ["//visibility:public"],
        ),
        # TODO(i30144): migrate to the canton repo
        #        sh_test(
        #            name = "validate-dar-%s" % target,
        #            srcs = ["src/validate.sh"],
        #            args = [
        #                "$(location @maven//:com_daml_daml_lf_repl_2_13)",
        #                "$(location :testing-dar-%s)" % target,
        #                "--dev" if lf_version_is_dev(target) else "",
        #            ],
        #            data = [
        #                "@maven//:com_daml_daml_lf_repl_2_13",
        #                "testing-dar-%s" % target,
        #            ],
        #            deps = [
        #                "@bazel_tools//tools/bash/runfiles",
        #            ],
        #        ),
    ]
    for target in ALL_LF_VERSIONS
]

filegroup(
    name = "testing-dars",
    srcs = ["testing-dar-%s" % version for version in ENGINE_LF_VERSIONS],
    visibility = ["//visibility:public"],
)
