-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0


module ACS where

import DA.Foldable (mapA_)
import Daml.Trigger.LowLevel

data TriggerState = TriggerState
  { activeAssets : [ContractId Asset]
  , successfulCompletions : Int
  , failedCompletions : Int
  , party : Party
  }

initState : Party -> ActiveContracts -> TriggerState
initState party (ActiveContracts events) = TriggerState
  { activeAssets = (foldl updateAcs [] events)
  , party = party
  , successfulCompletions = 0
  , failedCompletions = 0
  }
  where
    updateAcs : [ContractId Asset] -> Created -> [ContractId Asset]
    updateAcs acs created
      | Some (_, cid, _) <- fromCreated @Asset created = cid :: acs
      | otherwise = acs

-- | This is a very silly trigger for testing purposes:
-- We track the active Asset contracts
-- and we create a new AssetMirror contract whenever an Asset contract is created (but we do not archive them).
test : Trigger TriggerState
test = Trigger
  { initialState = \party _readAs acs -> pure $ initState party acs
  , update = update
  , registeredTemplates = AllTemplates
  , heartbeat = None
  }
  where
    update : Message -> TriggerRule TriggerState ()
    update (MCompletion c) =
      modify $ \state -> case c.status of
            Failed {} -> state { failedCompletions = state.failedCompletions + 1 }
            Succeeded {} -> state { successfulCompletions = state.successfulCompletions + 1 }
    update (MTransaction t) = do
      state <- get
      let (state', cmds) = case foldl updateEvent ([], state.activeAssets) (events t) of
            ([], acs) -> (state { activeAssets = acs }, [])
            (cmds, acs) ->
              ( state { activeAssets = acs }
              , [cmds]
              )
      put state'
      mapA_ submitCommands cmds
      where
        updateEvent : ([Command], [ContractId Asset]) -> Event -> ([Command], [ContractId Asset])
        updateEvent (cmds, acs) ev = case ev of
          CreatedEvent (fromCreated @Asset -> Some (_, assetId, asset)) ->
            let proposeMirror : Command = createCmd (AssetMirrorProposal { issuer = asset.issuer })
            in (proposeMirror :: cmds, assetId :: acs)
          CreatedEvent (fromCreated -> Some (_, proposalId, AssetMirrorProposal {})) ->
            let accept : Command = exerciseCmd proposalId Accept
            in (accept :: cmds, acs)
          ArchivedEvent (fromArchived @Asset -> Some (_, assetId)) ->
            (cmds, filter (/= assetId) acs)
          _ -> (cmds, acs)
    update MHeartbeat = pure ()

template Asset
  with
    issuer : Party
  where
    signatory issuer

template AssetMirror
  with
    issuer : Party
  where
    signatory issuer

template AssetMirrorProposal
  with
    issuer : Party
  where
    signatory issuer

    choice Accept : ContractId AssetMirror
      controller issuer
      do create AssetMirror { issuer = issuer }
