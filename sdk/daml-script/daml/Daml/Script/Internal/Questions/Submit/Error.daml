-- Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Script.Internal.Questions.Submit.Error
  ( DevErrorType (..)
  , ContractNotFoundAdditionalInfo -- Don't export constructors, users should not be able to rely on the structure.
  , isNotActive
  , SubmitError (..)
  , UpgradeErrorType (..)
  , CryptoErrorType (..)
  ) where

import Daml.Script.Internal.Questions.Util
import DA.NonEmpty
import DA.Text
import DA.Foldable (foldMap)
import DA.Exception
import DA.Fail

-- | MOVE Daml.Script
-- SCU related submission errors
data UpgradeErrorType
  = ValidationFailed with
        coid : AnyContractId
        srcTemplateId : TemplateTypeRep
        dstTemplateId : TemplateTypeRep
        srcPackageName : Text
        dstPackageName : Text
        originalSignatories : [Party]
        originalObservers : [Party]
        originalKeyOpt : Optional (AnyContractKey, [Party])
        recomputedSignatories : [Party]
        recomputedObservers : [Party]
        recomputedKeyOpt : Optional (AnyContractKey, [Party])
  | TranslationFailed with
        mCoid : Optional AnyContractId
        srcTemplateId : TemplateTypeRep
        dstTemplateId : TemplateTypeRep
        createArg : AnyTemplate
  | AuthenticationFailed with
        coid : AnyContractId
        srcTemplateId : TemplateTypeRep
        dstTemplateId : TemplateTypeRep
        createArg : AnyTemplate

-- | MOVE Daml.Script
-- Daml Crypto (Secp256k1) related submission errors
data CryptoErrorType
  = MalformedByteEncoding with
        value : Text
    | MalformedKey with
        keyValue : Text
    | MalformedSignature with
        signatureValue : Text
    | MalformedContractId with
        contractIdValue : Text
    deriving Show

-- | HIDE
-- Errors that will be promoted to SubmitError once stable - code needs to be kept in sync with SubmitError.scala
data DevErrorType
  = ChoiceGuardFailed
  | UnknownNewFeature -- ^ This should never happen - Update Scripts when you see this!
  deriving Show

-- | MOVE Daml.Script.Internal
-- Additional debugging information provided only by IDE Ledger
data ContractNotFoundAdditionalInfo
  = NotFound -- The generic "this contract does not exist"
  | NotActive with -- The contract did exist, but has since been archived
      additionalInfoCid : AnyContractId
  | NotEffective with -- The contract "will" exist, only thrown from the IDELedger where one can go backwards in time
      additionalInfoCid : AnyContractId
      effectiveAt : Text
  | NotVisible with -- The contract exists, but the readAs/actAs party union cannot see it
      additionalInfoCid : AnyContractId
      actAs : [Party]
      readAs : [Party]
      observers : [Party]
  deriving Show

-- | MOVE Daml.Script.Internal
-- Exacts nonactive contract ID from ContractNotFoundAdditionalInfo
isNotActive : ContractNotFoundAdditionalInfo -> Optional AnyContractId
isNotActive (NotActive {additionalInfoCid}) = Some additionalInfoCid
isNotActive _ = None

-- Must be kept up to date with SubmitError.scala
-- | MOVE Daml.Script
-- Errors that can be thrown by a command submission via `trySubmit`
data SubmitError
  = -- | Contract with given contract ID could not be found, and has never existed on this participant
    -- When run on Canton, there may be more than one contract ID, and additionalDebuggingInfo is always None
    -- On the other hand, when run on IDELedger, there is only ever one contract ID, and additionalDebuggingInfo is always Some
    ContractNotFound with
      unknownContractIds : NonEmpty Text
        -- ^ Provided as text, as we do not know the template ID of a contract if the lookup fails
      additionalDebuggingInfo : Optional ContractNotFoundAdditionalInfo
       -- ^ should always be None in Canton's case, see https://github.com/digital-asset/daml/issues/17154
  | -- | Contract with given contract key could not be found
    ContractKeyNotFound with
      contractKey : AnyContractKey
  | -- | No vetted package with given package name could be found
    UnresolvedPackageName with
      packageName : Text
  | -- | Generic authorization failure, included missing party authority, invalid signatories, etc.
    AuthorizationError with
      authorizationErrorMessage : Text -- We can't pass over the data at the moment, so we just give the full string error here, for anyone that wants to check it
  | -- | Failed to hash a contract
    ContractHashingError with
      contractId : AnyContractId
      dstTemplateId : TemplateTypeRep
      createArg : AnyTemplate
      errorMessage: Text
  | -- | Given disclosed contract key does not match the contract key of the contract on ledger.
    DisclosedContractKeyHashingError with
      contractId : AnyContractId
      expectedKey : AnyContractKey
      givenKeyHash : Text
  | -- | Attempted to create a contract with a contract key that already exists
    DuplicateContractKey with
      duplicateContractKey : Optional AnyContractKey
        -- ^ Canton will often not provide this key, IDELedger will
  | -- | Contract key lookup yielded different results
    InconsistentContractKey with
      contractKey : AnyContractKey
  | -- | Unhandled user thrown exception
    UnhandledException with
      exc : Optional AnyException
        -- ^ Errors more complex than simple records cannot currently be encoded over the grpc status. Such errors will be missing here.
  | -- | Transaction failure due to abort/assert calls pre-exceptions
    UserError with
      userErrorMessage : Text -- TODO[SW] Post LF 1.14, only `submitMustFail` can throw this. I'll refactor that later and likely remove this error
  | -- | Failure due to false result from `ensure`, strictly pre-exception.
    -- According to docs, not throwable with LF >= 1.14.
    -- On LF >= 1.14, a failed `ensure` will result in a `PreconditionFailed`
    -- exception wrapped in `UnhandledException`.
    TemplatePreconditionViolated
  | -- | Attempted to create a contract with empty contract key maintainers
    CreateEmptyContractKeyMaintainers with
      invalidTemplate : AnyTemplate
  | -- | Attempted to fetch a contract with empty contract key maintainers
    FetchEmptyContractKeyMaintainers with
      failedTemplateKey : AnyContractKey
  | -- | Attempted to exercise/fetch a contract with the wrong template type
    WronglyTypedContract with
      contractId : AnyContractId -- ^ Any contract Id of the actual contract
      expectedTemplateId : TemplateTypeRep
      actualTemplateId : TemplateTypeRep
  | -- | Attempted to use a contract as an interface that it does not implement
    ContractDoesNotImplementInterface with
      contractId : AnyContractId
      templateId : TemplateTypeRep
      interfaceId : TemplateTypeRep -- Consider InterfaceTypeRep from DA.Internal.Interface.AnyView.Types (so exposing it in stdlib)
  | -- | Attempted to use a contract as a required interface that it does not implement
    ContractDoesNotImplementRequiringInterface with
      contractId : AnyContractId
      templateId : TemplateTypeRep
      requiredInterfaceId : TemplateTypeRep
      requiringInterfaceId : TemplateTypeRep
  | -- | Attempted to compare values that are not comparable
    NonComparableValues
  | -- | Illegal Contract ID found in Contract Key
    ContractIdInContractKey with
      -- TODO[SW]: Update the ContractIdInContractKey scala error to include a TemplateId (which it does have when thrown) in separate PR
      -- Then we can uncomment this and add the plumbing
      -- contractKey : AnyContractKey
  | -- | Attempted to compare incomparable contract IDs. You're doing something very wrong.
    -- Two contract IDs with the same prefix are incomparable if one of them is local and the other non-local
    -- or if one is relative and the other relative or absolute with a different suffix.
    ContractIdComparability with
      globalExistingContractId : Text
        -- ^ We do not know the template ID at time of comparison.
  | -- | A value has been nested beyond a given depth limit
    ValueNesting with
        limit : Int
          -- ^ Nesting limit that was exceeded
  | -- | The transaction refers to locked contracts which are in the process of being created, transferred, or
    -- archived by another transaction. If the other transaction fails, this transaction could be successfully retried.
    LocalVerdictLockedContracts with
      localVerdictLockedContracts : [AnyContractId]
          -- ^ Locked contract ids
  | -- | The transaction refers to locked keys which are in the process of being modified by another transaction.
    LocalVerdictLockedKeys with
      localVerdictLockedKeys : [AnyContractKey]
          -- ^ Locked contract keys
  | -- | Upgrade exception
    UpgradeError with
      errorType : UpgradeErrorType
      errorMessage : Text
  | -- | Exception resulting from call to `failWithStatus`
    FailureStatusError with
      failureStatus : FailureStatus
  | -- | Crypto exceptions
    CryptoError with
      cryptoErrorType : CryptoErrorType
      cryptoErrorMessage : Text
  | -- | Development feature exceptions
    DevError with -- We just give the error's simple class name and the prettied message it generates. Scripts doesn't need anything more right now.
      devErrorType : DevErrorType
      devErrorMessage : Text
  | -- | Generic catch-all for missing errors.
    UnknownError with
      unknownErrorMessage : Text
  | -- | One of the above error types where the full exception body did not fit into the response, and was incomplete.
    TruncatedError with
      truncatedErrorType : Text
        -- ^ One of the constructor names of SubmitFailure except DevError, UnknownError, TruncatedError
      truncatedErrorMessage : Text

-- TODO(https://github.com/digital-asset/daml/issues/18328): improve error messages involving types
-- | MOVE Daml.Script
instance Show SubmitError where
  show err = case err of
    ContractNotFound { unknownContractIds, additionalDebuggingInfo } ->
      "Could not find contract with ID(s) " <> intercalate ", " (toList unknownContractIds) <> "."
      <> foldMap (\info -> " Additional debugging info: " <> show info) additionalDebuggingInfo
    ContractKeyNotFound contractKey -> "Could not find contract with given key"
    UnresolvedPackageName packageName -> "Could not find any vetted package with name " <> packageName
    AuthorizationError errMessage -> "Authorization failure: " <> errMessage
    ContractHashingError contractId _dstTemplateId _createArg msg ->
      "Failed to hash contract with ID " <> show contractId <> "\n with message \"" <> msg <> "\""
    DisclosedContractKeyHashingError contractId contractKey contractKeyHash ->
      "Disclosed contract with ID " <> show contractId <> " used incorrect payload - contract key hash: " <> contractKeyHash
    DuplicateContractKey contractKey -> "Attempted to create a contract with a contract key that is already in use"
    InconsistentContractKey contractKey -> "Inconsistent contract key between daml and ledger"
    UnhandledException None -> "An Exception was thrown and not caught"
    UnhandledException (Some exn) -> "An Exception was thrown and not caught - message: " <> (DA.Exception.message exn)
    UserError errMessage -> "User Error: " <> errMessage
    TemplatePreconditionViolated {} -> "Template precondition was violated"
    CreateEmptyContractKeyMaintainers _ -> "Attempted to create a contract with empty key maintainers"
    FetchEmptyContractKeyMaintainers contractKey -> "Attempted to fetch a contract with empty key maintainers"
    WronglyTypedContract contractId _ _ ->
      "Attempted to exercise/fetch a contract with the wrong template type - contract ID: " <> show contractId
    ContractDoesNotImplementInterface contractId _ _ ->
      "Attempted to use a contract as an interface that it does not implement - contract ID: " <> show contractId
    ContractDoesNotImplementRequiringInterface contractId _ _ _ ->
      "Attempted to use a contract as a required interface that it does not implement - contract ID: " <> show contractId
    NonComparableValues {} -> "Failed to compare values"
    ContractIdInContractKey {} -> "Illegal contract ID in contract key"
    ContractIdComparability contractId -> "Attempted to compare local and global contract IDs of the same contract - contract ID: " <> contractId
    ValueNesting limit -> "Exceeded maximum nesting depth for values - limit: " <> show limit
    LocalVerdictLockedContracts cids -> "Attempted to use locked contracts: " <> show cids
    LocalVerdictLockedKeys _ -> "Attempted to use locked contract keys"
    UpgradeError ty msg -> "UpgradeError: " <> show ty <> "\n with message \"" <> msg <> "\""
    FailureStatusError failureStatus -> "Failed with Status: " <> show failureStatus
    CryptoError ty msg -> "CryptoError: " <> show ty <> "\n with message \"" <> msg <> "\""
    DevError ty msg -> "DevError of type " <> show ty <> " and message \"" <> msg <> "\""
    UnknownError msg -> "Unknown error: " <> msg
    TruncatedError ty msg -> "TruncatedError of type " <> ty <> " and message \"" <> msg <> "\""

-- | MOVE Daml.Script
instance Show UpgradeErrorType where
  show err = case err of
    (ValidationFailed
        coid
        _srcTemplateId
        _dstTemplateId
        srcPackageName
        dstPackageName
        originalSignatories
        originalObservers
        _originalKeyOpt
        recomputedSignatories
        recomputedObservers
        _recomputedKeyOp) ->
      "Validation failed for contract "
          <> show coid
          <> " ( original package name: " <> show srcPackageName
          <> ", original signatories: " <> show originalSignatories
          <> ", original observers: " <> show originalObservers
          <> ", target package name:" <> show dstPackageName
          <> ", recomputed signatories: " <> show recomputedSignatories
          <> ", recomputed observers: " <> show recomputedObservers <> ")"
    TranslationFailed mCoid _srcTemplateId _dstTemplateId _createArg ->
      "Failed to load " <> (optional "a contract" (\coid -> "contract" <> show coid)) mCoid
    AuthenticationFailed coid _srcTemplateId _dstTemplateId _createArg ->
      "Authentication of contract " <> show coid <> " failed"