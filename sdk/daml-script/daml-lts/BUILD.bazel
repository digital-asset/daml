# Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@build_environment//:configuration.bzl", "ghc_version", "sdk_version")
load("//daml-lf/language:daml-lf.bzl", "COMPILER_LF_VERSIONS", "lf_version_default_or_latest")
load("//rules_daml:daml.bzl", "daml_compile")

# Note that daml-script-lts is split into 2 packages, daml-script-lts and daml-script-lts-stable
# See the internal and stable subdirectories respectively for these packages and their purpose.

# Copy the internal library over to top level, to keep a nice bazel path
[
    [
        copy_file(
            name = "daml-script-lts-{}".format(lf_version),
            src = "//daml-script/daml-lts/internal:daml-script-lts-{}.dar".format(lf_version),
            out = "daml-script-lts-{}.dar".format(lf_version),
            allow_symlink = True,
            visibility = ["//visibility:public"],
        ),
        copy_file(
            name = "daml-script-lts-stable-{}".format(lf_version),
            src = "//daml-script/daml-lts/stable:daml-script-lts-stable-{}.dar".format(lf_version),
            out = "daml-script-lts-stable-{}.dar".format(lf_version),
            allow_symlink = True,
            visibility = ["//visibility:public"],
        ),
    ]
    for lf_version in COMPILER_LF_VERSIONS
]

# For convenience, we define daml-script-lts.dar which is always compiled to the
# default stable version of LF2.
copy_file(
    name = "daml-script-lts",
    src = ":daml-script-lts-{}.dar".format(lf_version_default_or_latest("1")),
    out = "daml-script-lts.dar",
    allow_symlink = True,
    visibility = ["//visibility:public"],
)

filegroup(
    name = "daml-script-lts-dars",
    srcs = [
               "daml-script-lts-{}.dar".format(lf_version)
               for lf_version in COMPILER_LF_VERSIONS
           ] +
           [
               "daml-script-lts-stable-{}.dar".format(lf_version)
               for lf_version in COMPILER_LF_VERSIONS
           ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "daml-script-docs",
    srcs = [
        "//daml-script/daml-lts/internal:daml-script.json",
        ":daml-script-rst-template.rst",
        ":daml-script-index-template.rst",
        ":daml-script-hoogle-template.txt",
    ],
    outs = [
        "daml-script-rst.tar.gz",
        "daml-script-hoogle.txt",
        "daml-script-anchors.json",
    ],
    cmd = """
        $(location //compiler/damlc) -- docs \
            --output=daml-script-rst \
            --input-format=json \\
            --format=Rst \
            --template=$(location :daml-script-rst-template.rst) \
            --index-template=$(location :daml-script-index-template.rst) \\
            --hoogle-template=$(location :daml-script-hoogle-template.txt) \\
            --base-url=https://docs.daml.com/daml-script/api/ \\
            --output-hoogle=$(location :daml-script-hoogle.txt) \\
            --output-anchor=$(location :daml-script-anchors.json) \\
            $(location //daml-script/daml-lts/internal:daml-script.json)
        $(execpath //bazel_tools/sh:mktgz) $(location :daml-script-rst.tar.gz) daml-script-rst
    """,
    tools = [
        "//bazel_tools/sh:mktgz",
        "//compiler/damlc",
    ],
    visibility = ["//visibility:public"],
)
