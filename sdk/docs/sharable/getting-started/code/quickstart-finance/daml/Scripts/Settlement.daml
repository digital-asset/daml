module Scripts.Settlement where

import DA.Set (fromList, singleton)
import Daml.Script

-- INTERFACE DEPENDENCIES --
import Daml.Finance.Interface.Holding.V4.Holding qualified as Holding (I)
import Daml.Finance.Interface.Instrument.Token.V4.Factory qualified as TokenFactory (Create(..), I)
import Daml.Finance.Interface.Instrument.Token.V4.Types (Token(..))
import Daml.Finance.Interface.Settlement.V4.Batch qualified as Batch (Settle(..))
import Daml.Finance.Interface.Settlement.V4.Factory qualified as SettlementFactory (I)
import Daml.Finance.Interface.Settlement.V4.Instruction qualified as Instruction (Allocate(..), Approve(..))
import Daml.Finance.Interface.Settlement.V4.RouteProvider qualified as RouteProvider (I)
import Daml.Finance.Interface.Settlement.V4.Types (Allocation(..), Approval(..))
import Daml.Finance.Interface.Types.Common.V3.Types (AccountKey, HoldingStandard(..), Id(..), InstrumentKey(..))
import Daml.Finance.Interface.Util.V3.Common (qty)

-- IMPLEMENTATION DEPENDENCIES --
import Daml.Finance.Settlement.V4.Factory qualified as Settlement (Factory(..))
import Daml.Finance.Settlement.V4.RouteProvider.SingleCustodian (SingleCustodian(..))

import Workflow.CreditAccount qualified as CreditAccount (Accept(..), Request(..))
import Workflow.DvP qualified as DvP (Accept(..), Proposal(..))

import Scripts.Transfer (TransferState(..), runTransfer)

-- | Test script that
-- 1. executes the `runTransfer` script
-- 2. creates an instrument representing a generic Token
-- 3. credits a holding on the Token to Alice in her bank account
-- 4. atomically exchanges Alice's token holding against Bob's cash holding
runSettlement : Script SettlementState
runSettlement = do

  -- Execute the `runTransfer` script. Bob now holds USD 1000 in his account at the Bank.
  TransferState
    { alice
    ; bank
    ; bob
    ; aliceAccount
    ; bobAccount
    ; usdInstrument
    ; bobCashHoldingCid
    ; tokenFactoryCid
    } <- runTransfer

  -- Bank creates a token instrument
  let
    instrumentId = Id "TOKEN"
    instrumentVersion = "0"
    tokenInstrument = InstrumentKey with
      issuer = bank
      depository = bank
      id = instrumentId
      version = instrumentVersion
      holdingStandard = TransferableFungible

  now <- getTime

  submit bank do
    exerciseCmd tokenFactoryCid TokenFactory.Create with
      token = Token with
        instrument = tokenInstrument
        description = "Instrument representing units of a generic token"
        validAsOf = now
      observers = mempty

  -- Credit Alice's account with a token holding
  aliceRequestCid <- submit alice do
    createCmd CreditAccount.Request with
      account = aliceAccount
      instrument = tokenInstrument
      amount = 10.0
  aliceHoldingCid <- submit bank do exerciseCmd aliceRequestCid CreditAccount.Accept

  -- Setup a route provider
  -- This is used transform settlement `Step`s into a `RoutedStep`s using a single custodian
  -- ROUTE_PROVIDER_BEGIN
  routeProviderCid <- toInterfaceContractId @RouteProvider.I <$> submit bank do
    createCmd SingleCustodian with
      provider = bank; observers = fromList [alice, bob]; custodian = bank
  -- ROUTE_PROVIDER_END

  -- Setup a Settlement Factory facility
  -- This is used to generate settlement instructions from a list of `RoutedStep`s
  -- SETTLEMENT_FACTORY_BEGIN
  settlementFactoryCid <- toInterfaceContractId @SettlementFactory.I <$> submit bank do
    createCmd Settlement.Factory with
      provider = bank
      observers = fromList [alice, bob]
  -- SETTLEMENT_FACTORY_END

  -- Bob proposes to buy a token from Alice with USD
  -- DVP_PROPOSE_BEGIN
  dvpProposalCid <- submit bob do
    createCmd DvP.Proposal with
      id = "xccy trade"
      recQuantity = qty 10.0 tokenInstrument
      payQuantity = qty 1000.0 usdInstrument
      proposer = bob
      counterparty = alice
      routeProviderCid -- This is equivalent to writing routeProviderCid = routeProviderCid
      settlementFactoryCid
  -- DVP_PROPOSE_END

  -- DVP_ACCEPT_BEGIN
  (batchCid, recSettleInstructionCid, paySettleInstructionCid) <- submit alice do
    exerciseCmd dvpProposalCid DvP.Accept
  -- DVP_ACCEPT_END

  -- Settle the DvP Trade

  -- i. Bob allocates his asset, Alice approves by providing her account.
  (allocatedPaySettleInstructionCid, _) <- submit bob do
    exerciseCmd paySettleInstructionCid Instruction.Allocate with
      actors = singleton bob
      allocation = Pledge bobCashHoldingCid

  approvedPaySettleInstructionCid <- submit alice do
    exerciseCmd allocatedPaySettleInstructionCid Instruction.Approve with
      actors = singleton alice
      approval = TakeDelivery aliceAccount

  -- ii. Alice allocates her asset, Bob approves by providing his account.
  -- ALLOCATE_APPROVE_BEGIN
  (allocatedRecSettleInstructionCid, _) <- submit alice do
    exerciseCmd recSettleInstructionCid Instruction.Allocate with
      actors = singleton alice
      allocation = Pledge aliceHoldingCid

  approvedRecSettleInstructionCid <- submit bob do
    exerciseCmd allocatedRecSettleInstructionCid Instruction.Approve with
      actors = singleton bob
      approval = TakeDelivery bobAccount
  -- ALLOCATE_APPROVE_END

  -- iii. Bob executes the settlement.
  -- SETTLE_BEGIN
  [bobHoldingCid, aliceHoldingCid] <- submit bob do
    exerciseCmd batchCid Batch.Settle with
      actors = singleton bob
  -- SETTLE_END

  pure SettlementState with
    bank
    bob
    bobAccount
    usdInstrument
    tokenInstrument
    routeProviderCid
    settlementFactoryCid
    tokenFactoryCid
    bobHoldingCid = toInterfaceContractId bobHoldingCid

-- | Helper container used to transfer state to the next script.
data SettlementState = SettlementState
  with
    bank : Party
    bob : Party
    bobAccount : AccountKey
    usdInstrument : InstrumentKey
    tokenInstrument : InstrumentKey
    routeProviderCid : ContractId RouteProvider.I
    settlementFactoryCid : ContractId SettlementFactory.I
    tokenFactoryCid : ContractId TokenFactory.I
    bobHoldingCid : ContractId Holding.I
