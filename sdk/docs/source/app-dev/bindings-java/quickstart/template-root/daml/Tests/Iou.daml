-- Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE ApplicativeDo #-}

module Tests.Iou where

import DA.Assert
import Daml.Script

import Iou

iou_test = do
  bank <- allocateParty "Acme Bank"
  alice <- allocateParty "Alice"
  bob <- allocateParty "Bob"
  charlie <- allocateParty "Charlie"

  let
    issuer = bank
    currency = "USD"

  -- Bank creates IOU transfers for Alice and Bob.
  iouTransferAliceCid <- submit bank $
    createAndExerciseCmd
      Iou with
        issuer
        owner = issuer
        currency
        amount = 100.0
        observers = []
      Iou_Transfer with
        newOwner = alice

  iouTransferBobCid <- submit bank do
    createAndExerciseCmd
      Iou with
        issuer
        owner = issuer
        currency
        amount = 30.0
        observers = []
      Iou_Transfer with
        newOwner = bob

  iouAliceCid <- submit alice do
    exerciseCmd iouTransferAliceCid IouTransfer_Accept

  iouBobCid <- submit bob do
    exerciseCmd iouTransferBobCid IouTransfer_Accept

  -- Alice splits the IOU and transfers part of it to Bob.
  split <- submit alice do
    exerciseCmd iouAliceCid Iou_Split with splitAmount = 40.0
  iouTransferCid <- submit alice do
    exerciseCmd (fst split) Iou_Transfer with newOwner = bob

  -- Bob accepts and merges it with the one he already has.
  iouOtherCid <- submit bob do
    exerciseCmd iouTransferCid IouTransfer_Accept

  iouTotalCid <- submit bob do
    exerciseCmd iouBobCid Iou_Merge with otherCid = iouOtherCid

  -- Assert things are as they should be.
  do
    Some aliceIou <- queryContractId alice (snd split)
    assert $ aliceIou == Iou with
      issuer = bank
      owner = alice
      currency = "USD"
      amount = 60.0
      observers = []

  do
    Some bobIou <- queryContractId bob iouTotalCid
    assert $ bobIou == Iou with
      issuer = bank
      owner = bob
      currency = "USD"
      amount = 70.0
      observers = []

  -- Issuers can archive IOUs they own.
  iouBankCid <- submit bank do
    createCmd Iou with
      issuer = bank
      owner = bank
      currency = "USD"
      amount = 100.0
      observers = []

  submit bank do exerciseCmd iouBankCid Archive
  optIou <- queryContractId bank iouBankCid
  optIou === None

  -- Alice can't create IOUs that are backed by Acme Bank.
  submitMustFail alice do
    createCmd Iou with
      issuer = bank
      owner = alice
      currency = "USD"
      amount = 50.0
      observers = []

  -- Acme Bank can't create IOUs with a negative amount.
  submitMustFail bank do
    createCmd Iou with
      issuer = bank
      owner = alice
      currency = "USD"
      amount = -20.0
      observers = []

  submitMustFail bank do
    createCmd Iou with
      issuer = bank
      owner = alice
      currency = "USD"
      amount = -20.0
      observers = []

  -- SplitAmount needs to be between 0 and amount.
  submitMustFail alice do exerciseCmd (snd split) Iou_Split with splitAmount = 80.0

  -- Double spend gets prevented by archiving the contract.
  submitMustFail alice do exerciseCmd (fst split) Iou_Transfer with newOwner = charlie

  -- Similarly, a merged contract gets archived as well.
  submitMustFail bank do exerciseCmd iouOtherCid Iou_Transfer with newOwner = charlie

  -- Only the owner can exercise the transfer choice.
  submitMustFail bank do exerciseCmd (snd split) Iou_Transfer with newOwner = charlie

  -- Issuer can't archive something Bob owns.
  submitMustFail bank do exerciseCmd (fst split) Archive

  -- Issuer can't archive something Alice owns.
  submitMustFail bank do exerciseCmd (snd split) Archive

  -- Alice didn't disclose her remaining IOUs to Bob.
  optIou <- queryContractId bob (snd split)
  optIou === None

  -- Only matching IOUs can be merged.
  iouTransferEURCid <- submit bank do
    createAndExerciseCmd
      Iou with
        issuer = bank
        owner = bank
        currency = "EUR"
        amount = 60.0
        observers = []
      Iou_Transfer with
        newOwner = alice

  iouEURCid <- submit alice do
    exerciseCmd iouTransferEURCid IouTransfer_Accept
  submitMustFail alice do
    exerciseCmd (snd split) Iou_Merge with otherCid = iouEURCid
