# Copyright (c) 2025 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
load("//bazel_tools:haskell.bzl", "da_haskell_library", "da_haskell_repl", "da_haskell_test")
load("@os_info//:os_info.bzl", "is_windows")
load("@build_environment//:configuration.bzl", "mvn_version")

# A REPL with all tests, used as bios for HLS
da_haskell_repl(
    name = "tests@ghci",
    testonly = True,
    deps = [
        ":integration-test-utils",
        ":integration-tests",
        ":quickstart-tests",
    ],
)

genrule(
    name = "integration-tests-mvn",
    srcs = [
        "@mvn_dev_env//:mvn",
        "@maven//:com_daml_bindings_java",
        "//language-support/java/codegen:binary.jar",
        "//language-support/java/codegen:binary_pom.xml",
        "//language-support/js/codegen:binary.jar",
        "//language-support/js/codegen:binary_pom.xml",
        "@maven//:com_daml_rs_grpc_bridge",
        "@maven//:io_grpc_grpc_stub",
        "@maven//:com_google_protobuf_protobuf_java",
        "//docs:quickstart-java.tar.gz",
    ],
    outs = ["integration-tests-mvn.tar"],
    cmd = """
      set -euo pipefail
      TMP_DIR=$$(mktemp -d)
      MVN_DB="$$TMP_DIR/m2"
      MVN=($(locations @mvn_dev_env//:mvn))
      MVN="$${{MVN[0]}}"
      install_mvn() {{
        local POM_ARG="-DgeneratePom=true"
        if [[ "$$#" -eq 4 ]]; then
          local POM_ARG="-DpomFile=$$4"
        fi
        "$$MVN" -q install:install-file \\
          -Dmaven.repo.local=$$MVN_DB \\
          "-DgroupId=$$1" \\
          "-DartifactId=$$2" \\
          "-Dfile=$$3" \\
          "$$POM_ARG" \\
          -Dpackaging=jar \\
          -Dversion={mvn}
      }}
      tar xf $(location //docs:quickstart-java.tar.gz) -C "$$TMP_DIR"
      install_mvn \\
        "com.daml" "bindings-java" \\
        $(location @maven//:com_daml_bindings_java)
      install_mvn \\
        "io.grpc" "grpc-stub" \\
        $(location @maven//:io_grpc_grpc_stub)
      install_mvn \\
        "com.google.protobuf" "protobuf-java" \\
        $(location @maven//:com_google_protobuf_protobuf_java)
      install_mvn \\
        "com.daml" "codegen-java" \\
        $(location //language-support/java/codegen:binary.jar) \\
        $(location //language-support/java/codegen:binary_pom.xml)
      install_mvn \\
        "com.daml" "codegen-js" \\
        $(location //language-support/js/codegen:binary.jar) \\
        $(location //language-support/js/codegen:binary_pom.xml)
      install_mvn \\
        "com.daml" "rs-grpc-bridge" \\
        $(location @maven//:com_daml_rs_grpc_bridge)
      "$$MVN" -q -Dmaven.repo.local=$$MVN_DB -f "$$TMP_DIR/quickstart-java/pom.xml" dependency:resolve dependency:resolve-plugins
      $(execpath //bazel_tools/sh:mktar) $@ -C $$(dirname $$MVN_DB) $$(basename $$MVN_DB)
    """.format(mvn = mvn_version),
    tools = ["//bazel_tools/sh:mktar"],
)

os_dependent_libraries = [] if is_windows else [
    "//language-support/js/daml-types:npm_package",
    "@script_nix//:bin/script",
]

da_haskell_library(
    name = "integration-test-utils",
    srcs = ["src/DA/Daml/Assistant/IntegrationTestUtils.hs"],
    hackage_deps = [
        "aeson",
        "base",
        "bytestring",
        "conduit",
        "conduit-extra",
        "extra",
        "filepath",
        "hashable",
        "tasty",
        "text",
        "unordered-containers",
    ],
    src_strip_prefix = "src",
    visibility = ["//visibility:public"],
    deps = [
        "//libs-haskell/bazel-runfiles",
        "//libs-haskell/da-hs-base",
        "//libs-haskell/test-utils",
    ],
)

da_haskell_test(
    name = "integration-tests",
    timeout = "long",
    srcs = [
        "src/DA/Daml/Assistant/IntegrationTests.hs",
    ],
    args = [
        "$(location //:yarn)",
    ],
    data = [
        "//release:dpm-sdk-release-tarball",
        "//release:sdk-release-tarball",
        "@local_jdk//:bin/java.exe" if is_windows else "@local_jdk//:bin/java",
        "//:yarn",
        "//compiler/damlc/tests:generate-simple-dalf",
    ] + os_dependent_libraries,
    # marked flaky for mvn originally; still flaky now? #14281
    flaky = False,
    hackage_deps = [
        "aeson",
        "base",
        "bytestring",
        "extra",
        "filepath",
        "http-client",
        "http-types",
        "lens",
        "lens-aeson",
        "monad-loops",
        "process",
        "stm",
        "tasty",
        "tasty-hunit",
        "text",
        "vector",
    ],
    main_function = "DA.Daml.Assistant.IntegrationTests.main",
    tags = ["cpu:2"],
    visibility = ["//visibility:public"],
    deps = [
        ":integration-test-utils",
        "//daml-assistant/daml-helper:daml-helper-lib",
        "//language-support/hs/bindings:hs-ledger",
        "//libs-haskell/bazel-runfiles",
        "//libs-haskell/da-hs-base",
        "//libs-haskell/test-utils",
        "//sdk-version/hs:sdk-version-lib",
    ],
)

da_haskell_test(
    name = "quickstart-tests",
    timeout = "long",
    srcs = [
        "src/DA/Daml/Assistant/QuickstartTests.hs",
    ],
    data = [
        ":integration-tests-mvn",
        "//release:sdk-release-tarball",
        "@local_jdk//:bin/java.exe" if is_windows else "@local_jdk//:bin/java",
        "//compiler/damlc/tests:generate-simple-dalf",
        "@mvn_dev_env//:mvn",
        "@tar_dev_env//:tar",
    ] + os_dependent_libraries,
    # Iâ€™m sure the mvn stuff will be flaky.
    flaky = True,
    hackage_deps = [
        "base",
        "bytestring",
        "conduit",
        "extra",
        "filepath",
        "http-client",
        "http-types",
        "process",
        "tasty",
        "tasty-hunit",
    ],
    main_function = "DA.Daml.Assistant.QuickstartTests.main",
    # TODO(https://github.com/DACH-NY/canton/issues/30144): fix or migrate this test to the assembly repo
    tags = [
        "cpu:1",
        "manual",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":integration-test-utils",
        "//daml-assistant:daml-project-config",
        "//daml-assistant/daml-helper:daml-helper-lib",
        "//language-support/hs/bindings:hs-ledger",
        "//libs-haskell/bazel-runfiles",
        "//libs-haskell/da-hs-base",
        "//libs-haskell/test-utils",
        "//sdk-version/hs:sdk-version-class-lib",
    ],
)
