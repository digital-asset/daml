# Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

load(
    "//bazel_tools:scala.bzl",
    "da_scala_binary",
    "da_scala_library",
)
load("@os_info//:os_info.bzl", "is_windows")
load(":util.bzl", "deps")

scala_deps = [
    "@maven//:org_apache_pekko_pekko_http_spray_json",
    "@maven//:com_typesafe_scala_logging_scala_logging",
    "@maven//:io_spray_spray_json",
]

scala_runtime_deps = [
    "@maven//:org_apache_pekko_pekko_slf4j",
    "@maven//:org_tpolecat_doobie_postgres",
]

runtime_deps = [
    "@maven//:ch_qos_logback_logback_classic",
    "@maven//:ch_qos_logback_logback_core",
    "@maven//:io_grpc_grpc_netty_shaded",
    "@maven//:org_xerial_sqlite_jdbc",
]

da_scala_binary(
    name = "sdk",
    srcs = glob(["src/main/scala/**/*.scala"]),
    main_class = "com.daml.sdk.SdkMain",
    resources = glob(["src/main/resources/**/*"]),
    scala_deps = scala_deps,
    scala_runtime_deps = scala_runtime_deps,
    # Navigator resources trigger this incorrectly
    unused_dependency_checker_mode = "off",
    visibility = ["//visibility:public"],
    runtime_deps = runtime_deps,
    deps = deps("ce"),
)

da_scala_binary(
    name = "sdk_ee",
    srcs = glob(["src/main/scala/**/*.scala"]),
    main_class = "com.daml.sdk.SdkMain",
    resources = glob(["src/main/resources/**/*"]),
    scala_deps = scala_deps,
    scala_runtime_deps = scala_runtime_deps,
    tags = ["ee-jar-license"],
    # Navigator resources trigger this incorrectly
    unused_dependency_checker_mode = "off",
    visibility = ["//visibility:public"],
    runtime_deps = runtime_deps + [
        "@maven//:com_oracle_database_jdbc_ojdbc8",
    ],
    deps = deps("ee"),
)

# This is deliberately an sh_test on the actual SDK jars to test
# that the class paths end up being resolved properly and we really
# have the EE artifacts when we want to and only then.
sh_test(
    name = "validate",
    srcs = ["validate.sh"],
    args = [
        "$(location @local_jdk//:bin/java.exe)" if is_windows else "$(location @local_jdk//:bin/java)",
        "$(location :sdk_distribute.jar)",
        "$(location :sdk_ee_distribute.jar)",
    ],
    data = [
        ":sdk_distribute.jar",
        ":sdk_ee_distribute.jar",
        "@local_jdk//:bin/java.exe" if is_windows else "@local_jdk//:bin/java",
    ],
    deps = [
        "@bazel_tools//tools/bash/runfiles",
    ],
)
